module generator/interpreter/sorts-to-java

imports
  libjava-front
  include/ds
  generator/interpreter/gen-interpreter
  names
  names.manual
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/nabl/-
  lib/editor-common.generated
  lib-ds
  
signature overlays
  
  JAVA_INTERFACE_MULTIPLE(pkgdec, imports, name, extended-ifaces) =
      CompilationUnit(
      Some(pkgdec)
      , imports
      , [ InterfaceDec(
            InterfaceDecHead(
              [Public()]
            , Id(name)
            , None()
            , Some(
                ExtendsInterfaces(extended-ifaces)
              )
            )
          , []
          )
        ]
      )

rules /* generate interfaces for declared sorts */

  sorts-to-java-interpreter:
    Module(mod, body*) -> [semanticcomponent-interface*, language-sort-interface*]
    with
      language-decl* := <collect-all(?SortDecl(_) + ?InjDecl(_, _))> body*;
      language-decl-pair* := <map(match-with-injected-sort(|language-decl*))> language-decl*;
      language-sort-pair* := <map((get-sort-name, map(get-sort-name)); add-term-sort)> language-decl-pair*;
    	internal-sort* := <collect-all(\ d@InternalSortDecl(_) -> (<get-sort-name> d, []) \; add-term-sort)> body*;
    	language-sort-interface* := <map(sort-to-java-interpreter)> [language-sort-pair*, internal-sort*]
    with
      semanticcomponent-sort* := <collect-all(?SemanticComponent(_); get-sort-name)> body*;
      semanticcomponent-interface* := <map(semanticcomponent-to-java-interpreter)> semanticcomponent-sort*
  
  match-with-injected-sort(|decl*):
    sdec -> (sdec, ssdec*)
    where
      srt := <get-sort-name> sdec
    with
      sdef := <get-annos; fetch-elem(?Def(_))> srt;
      ssdec* := <filter(where(?InjDecl(_, <id>); nabl-collect-one-resolved-def; ?sdef))> decl*
  
  add-term-sort:
    (srt, srt*@[_|_]) -> (srt, srt*)
  
  add-term-sort:
    (srt, []) -> (srt, ["Term"])
  
  // backend for Sort -> Java
  sort-to-java-interpreter:
    (srt, srt*) ->
      JAVA_INTERFACE_MULTIPLE(
        |[ package x_pkgname; ]|
        ,  [ |[ import org.metaborg.meta.interpreter.framework.*; ]| ]
        , $[I_[srt]]
        , <map(get-sort-java-name; string-to-java-iface-name)> srt*
      )
   with
    x_pkgname := <gen-package-name>
  
  semanticcomponent-to-java-interpreter:
    srt ->
      JAVA_INTERFACE_MULTIPLE(
        |[ package x_pkgname; ]|
        , [ |[ import org.metaborg.meta.interpreter.framework.*; ]| ]
        , $[I_[srt]], [<string-to-java-iface-name> "I_PersistentMap"])
    with
      x_pkgname := <gen-package-name>
  
  string-to-java-iface-name:
    s -> InterfaceType(TypeName(Id(s)), None())
    where
      is-string
  
  get-sort-java-name = get-native-sort-java-name <+ get-user-sort-java-name
  
  get-native-sort-java-name:
    "Term" -> <get-basesort-java-name>
    
  get-basesort-java-name = !$[I_[<language>]Node]

  get-native-sort-java-name:
    "Int" -> "int"

  get-native-sort-java-name:
    "String" -> "String"
  
  get-native-sort-java-name:
    "Value" -> "Value"
  
  get-native-sort-java-name:
    "Map" -> "I_InterpreterFrame"

  get-native-sort-java-name = not(is-string); get-sort-name; get-native-sort-java-name
  
  get-user-sort-java-name:
    term -> $[I_[term]]
    where
      is-string
  
  get-user-sort-java-name = not(is-string); get-sort-name; get-user-sort-java-name

  is-sort-native = not(is-string); get-sort-name; is-sort-native
  
  is-sort-native = get-sort-name; get-sort-kind => SemanticComponent()
  
  is-sort-native = is-sort-string + is-sort-int
  
  is-sort-string = try(not(is-string); get-sort-name); ?"String"
  
  is-sort-int = try(not(is-string); get-sort-name); ?"Int"

