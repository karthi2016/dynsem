module coq-semantics/main

imports
  libstratego-gpp
  coq-semantics/-
  coq-semantics/coq-syntax/-
  lib/runtime/index/-
  lib/runtime/nabl/-
  lib/runtime/properties/-
  lib/runtime/task/-
  names
  lib/editor-common.generated
  include/ds

rules
  
  generate-coq:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      task-setup(|project-path)
    ; filename := <guarantee-extension(|"v")> path
    ; coq-file := <strip-annos; module-to-coq> ast
    with
       result := <parenthesize-Coq; prettyprint-Start; debug(|"C"); box2text-string(|100)> coq-file
    <+ <bottomup(try(not(is-string) ; not(is-list) ; not(prettyprint-example) ; debug(!"cannot pp ")))> coq-file
    ;  result := ""

rules
  
  module-to-coq:
  	Module(name, rule*) -> [IND_SEMANTICS(definition*)]
		where
		  rule'*      := <filter(?Rules(<id>)); concat> rule*
		; definition* := <foldl(fold-rule); Fst; reverse> (rule'*, ([], []))

rules
  
  rule-to-coq(|n*):
    RuleW(formula, prem*) -> (DECL(name', <union> (p-bind*, f-bind*), type), [(name, count), n*])
    where
      (prem'*, p-bind*, i) := <foldl(fold-prem)> (prem*, ([], [], 0))
    ; (t', f-bind*, _)     := <formula-to-coq(|i)> formula
    ; type                 := <foldl(fold-type)> (prem'*, t')
    ; name                 := <formula-to-name> formula
    ; count                := <lookup; inc <+ !0> (name, n*)
    ; name'                := $[[name][count]C_sem]
  
  rule-to-coq(|n*):
  	Rule(prem*,_,formula) -> <rule-to-coq(|n*)> RuleW(formula, prem*)
  	
  rule-to-coq(|n*):
    Axiom(formula) -> <rule-to-coq(|n*)> RuleW(formula, [])

rules // Fold rules
  
  fold-rule:
    (r, (p*, n*)) -> (p'*, n'*)
    where
      (p'*, n'*) := <rule-to-coq(|n*) < !([<Fst> | p*], <Snd>) + !(p*, n*)> r
  
  fold-prem:
    (Formula(p), (p*, b*, i)) -> ([p' | p*], <union> (b*, b'*), i')
    where
      (p', b'*, i') := <formula-to-coq(|i)> p
  
  fold-type: // Composes parts of a type
    (x, y) -> Arrow(x, y)

overlays

  IND_SEMANTICS(terms) =
		Inductive(
      [ Induc(
          "semantics_cbn"
        , None()
        , Arrow(
            Ref("Env")
          , Arrow(Ref("term"), Arrow(Ref("value"), Prop()))
          )
        , ListDeclBar(terms)
        )
      ]
    )

  DECL(name, binds, type) =
		ConsDecl(
		  name
		, binds
		, type
    )
    
	CONS(hd, tl) = APPLY2(Ref("cons"), hd, tl)
	PAIR(a, b) = APPLY2(Ref("pair"), a, b)
	
	APPLY2(constr, fst, snd) =
	  Apply(
	    Apply(
	      constr
	    , fst
	    )
	  , snd
	  )