module coq-semantics/formula

imports
  libstratego-gpp
  coq-semantics/-
  lib/runtime/index/-
  lib/runtime/nabl/-
  lib/runtime/properties/-
  lib/runtime/task/-
  names
  lib/editor-common.generated
  include/ds

rules // Formula
  
  formula-to-coq(|i):
    Relation(r, src, rel, dst) -> (SEMANTICS_CBN(env, src', dst'), bind*, i-dst)
    where
      (env , b-env*, i-env) := <reads-to-coq(|i)> r
    ; (dst', b-dst*, i-dst) := <src-dst-to-coq(|i-env)> dst
    ; (src', b-src*, i-src) := <src-dst-to-coq(|i-dst)> src
    ; bind*                 := <union> (b-env*, <union> (b-src*, b-dst*))

  formula-to-coq(|i):
    TermEq(MapSelect(Var(env), Var(x)), rhs) -> (GET_ENV(x, env, e, env'), bind*, i)
    where
      Con("T", [Var(e), Var(env')]) := rhs
    ; bind* := <map(!Bind(<id>))> [env, x, e, env']

  formula-to-coq(|i):
    TermEq(Var(x), t) -> (COQ_EQ(Ref(x), t'), bind*, i')
    where
      (t', b*, i') := <term-to-coq(|i)> t
    ; bind*        := <union> ([Bind("env")], <union> ([Bind(x)], b*))
  
  formula-to-coq(|i):
    TermNeq(Var(x), t) -> (COQ_NEQ(Ref(x), t'), bind*, i')
    where
      (t', b*, i') := <term-to-coq(|i)> t
    ; bind*        := <union> ([Bind("env")], <union> ([Bind(x)], b*))
  
  formula-to-name = ?Relation(_, <id>, _, _); (?Source(<id>, _) <+ ?Source(<id>)); ?Con(<id>, _)
  
rules // Environments rules
  
  reads-to-coq(|i):
    Reads(lbl*) -> <foldl(fold-lbl)> (lbl*, ((), [], i))
    
  reads-to-coq(|i):
    NoReads() -> (Ref("env"), [Bind("env")], i)
      
rules // Folding rules
  
  fold-lbl:
    (LabelComp(_, e), (p , b*, i)) -> (n, <union> (b*, b'*), i')
    where
      (e', b'*, i') := <term-to-coq(|i)> e
    ; n             := <?() < !e' + !Product(e', p)> p

overlays
  COQ_NEQ(e1, e2) = Apply(Ref("not"), COQ_EQ(e1, e2))

  COQ_EQ(e1, e2) = APPLY2(Ref("eq"), e1, e2)

  GET_ENV(x, env, e, env') = APPLY2(APPLY2(Ref("get_env"), Ref(x), Ref(env)), Ref(e), Ref(env'))
  
	SEMANTICS_CBN(env, t1, t2) = APPLY2(Apply(Ref("semantics_cbn"), env), t1, t2)