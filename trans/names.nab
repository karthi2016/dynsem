module names

imports
  include/ds
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/nabl/-
	
namespaces Module Variable Constructor Sort


properties
  expected-type of Term : Sort
  sort-annos of Sort : SignatureAnnos
  cons-annos of Constructor : SignatureAnnos

binding rules

  Module(name, section*) :
    defines Module name
    scopes Variable, Constructor, Sort
    implicitly defines Sort "Term"
      of cons-annos SignatureAnnos([NativeAnno()])
    implicitly defines Sort "Int"
      of cons-annos SignatureAnnos([NativeAnno()])
    implicitly defines Sort "String"
      of cons-annos SignatureAnnos([NativeAnno()])
    implicitly defines Sort "Value"
      of cons-annos SignatureAnnos([NativeAnno(), ValueAnno()])
    implicitly defines Sort "PMap"
      of cons-annos SignatureAnnos([NativeAnno()])
    
  v@Var(x) :
  	defines Variable x of type t
  	where v has expected-type t
  
  VarRef(x) :
  	refers to Variable x
  	
  Axiom(conc) :
    scopes Variable
 
  Rule(prem*, _, conc) :
  	scopes Variable

  RuleW(conc, prem*) :
  	scopes Variable

  SortDecl(ty, annos) :
  	defines Sort ty
  	 of cons-annos annos
  
  InjDecl(ty, parent, annos) :
    defines Sort ty
      of cons-annos annos
    refers to Sort parent
  
  SimpleSort(ty) :
  	refers to Sort ty

  ConsDecl(c, ty*, ty, annos) :
  	defines Constructor c
  	 of type (ty*, ty)
  	 of cons-annos annos
  	  	
  Con(c, t*) :
  	refers to Constructor c
  	
  LabelComp(s, _) :
    refers to Sort s
 

  	
