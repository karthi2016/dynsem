module names

imports
  include/ds
  lib/nabl/-
	
namespaces Module Variable Constructor Sort Parent


properties
  expected-type of Term : Sort
  sort-annos of Sort : Term

binding rules

  Module(name, section*) :
    defines Module name
    scopes Variable, Constructor, Sort
    implicitly defines Sort "term"
      of sort-annos SignatureAnnos([NativeAnno()])
    implicitly defines Sort "int"
      of sort-annos SignatureAnnos([NativeAnno()])
    implicitly defines Sort "string"
      of sort-annos SignatureAnnos([NativeAnno()])
    implicitly defines Sort "value"
      of sort-annos SignatureAnnos([NativeAnno(), ValueAnno()])
    implicitly defines Sort "E"
      of sort-annos SignatureAnnos([NativeAnno()])
    
  v@Var(x) :
  	defines Variable x of type t
  	where v has expected-type t
  
  VarRef(x) :
  	refers to Variable x
  	
  Axiom(conc) :
    scopes Variable
 
  Rule(prem*, _, conc) :
  	scopes Variable

  RuleW(conc, prem*) :
  	scopes Variable

  SortDecl(ty, annos) :
  	defines Sort ty
  	 of sort-annos annos
  
  InjDecl(ty, parent, annos) :
    defines Sort ty
      of sort-annos annos
    refers to Sort parent
  
  SimpleSort(ty) :
  	refers to Sort ty

  ConsDecl(c, ty*, ty, _) :
  	defines Constructor c of type (ty*, ty)
  	  	
  Con(c, t*) :
  	refers to Constructor c
  	
  LabelComp(s, _) :
    refers to Sort s
 

  	
