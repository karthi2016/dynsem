module names

imports
  include/ds
  lib/nabl/-
	
namespaces Module Variable Constructor Sort Parent


properties
  expected-type of Term : Sort

binding rules

  Module(name, section*) :
  	defines Module name
  	scopes Variable, Constructor, Sort
  	
  v@Var(x) :
  	defines Variable x of type t
  	where v has expected-type t
  
  VarRef(x) :
  	refers to Variable x
  	
  Axiom(conc) :
    scopes Variable
 
  Rule(prem*, _, conc) :
  	scopes Variable

  RuleW(conc, prem*) :
  	scopes Variable

  SortDecl(ty, _) :
  	defines Sort ty
  
  InjDecl(ty, parent, _) :
    defines Sort ty
    refers to Sort parent
  
  SimpleSort(ty) :
  	refers to Sort ty

  ConsDecl(c, ty*, ty, _) :
  	defines Constructor c of type (ty*, ty)
  	  	
  Con(c, t*) :
  	refers to Constructor c
  	
  LabelComp(s, _) :
    refers to Sort s
 

  	
