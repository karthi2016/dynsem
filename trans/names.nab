module names

imports
  include/ds
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/nabl/-
	names.manual
	
namespaces
  Module
  Variable
  Constructor
  Sort


properties
  expected-type of Term : Sort
  sort-kind of Sort : SortKind // Language(), SemanticComponent(), Internal()
  cons-kind of Constructor : ConsKind // Language(), Value(), NativeOperator(), Internal()

binding rules // module and imports

  Module(name, section*) :
    defines Module name
    scopes Variable, Constructor, Sort
    implicitly defines Sort "Term"
      of type SimpleSort("Term")
      of sort-kind Internal()
    implicitly defines Sort "Int"
      of type SimpleSort("Int")
      of sort-kind Internal()
    implicitly defines Sort "String"
      of type SimpleSort("String")
      of sort-kind Internal()
    implicitly defines Sort "Value"
      of type SimpleSort("Value")
      of sort-kind Internal()

  Import(name):
  	imports Sort, Constructor from Module name

binding rules

  v@Var(x) :
    defines Variable x of type t
      where v has expected-type t
  
  VarRef(x) :
  	refers to Variable x
  	
  Axiom(conc) :
    scopes Variable
 
  Rule(prem*, _, conc) :
  	scopes Variable

  RuleW(conc, prem*) :
  	scopes Variable

binding rules // sorts 

  SimpleSort(ty) :
  	refers to Sort ty

  SortDecl(ty) :
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind Language()
  
  InjDecl(ty, parent) :
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind Language()
    refers to Sort parent

  SemanticComponent(ty) :
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind SemanticComponent()
  
  InternalSortDecl(ty) :
    defines Sort ty
      of sort-kind Internal()

binding rules // constructors

  Con(c, t*) :
  	refers to Constructor c

  ConsDecl(c, ty*, ty) :
    defines Constructor c
      of type (ty*, ty)
      of cons-kind Language()

  NativeOpDecl(c, ty*, ty):
    defines Constructor c
      of type (ty*, ty)
      of cons-kind NativeOperator()
  
  ValueDecl(c, ty*):
    defines Constructor c
      of type (ty*, "Value")
      of cons-kind Value()
  
  InternalConsDecl(c, ty*, ty) :
    defines Constructor c
      of type (ty*, ty)
      of cons-kind Internal()

binding rules // others

  LabelComp(s, _) :
    refers to Sort s
 

  	
