module sugar

imports
	include/ds
	
rules // desugaring
	
  desugar-all = innermost(desugar); alltd(rules-concat)
    
  desugar :
    NoReads() -> Reads([])
   
  desugar :
    Source(lhs) -> Source(lhs, [])
    
  desugar :
    Target(rhs) -> Target(rhs, [])
    
  desugar :
    Dynamic() -> DynamicEmitted([])
    
  desugar :
    Axiom(form) -> Rule([], "---------", form)

  desugar :
    Strict(con,params1,params2) -> list
     where Cons(_,Cons(_,_)) := params1;
           list := <map( \ param -> Strict(con,[param],params2) \)> params1
     
  rules-concat :
  	 Rules(list) -> Rules(list1)
  	 where list1 := <map(?Cons(_,_) <+ ( \ n -> Cons(n,Nil()) \)); concat> list
    
rules // sugaring
  
  sugar-all = innermost(sugar)
  
  sugar :
  	Rule([],infer,conc) -> Axiom(conc)

  sugar :
  	Reads([]) -> NoReads()

  sugar :
  	DynamicEmitted([]) -> Dynamic()

  sugar :
  	Target(term,[]) -> Target(term)

  sugar :
  	Source(term,[]) -> Source(term)
  
  
  
          
