module Signatures

imports
  Common
  ds

context-free syntax

  ModuleSection.Signatures = <
    signature
        <{SignatureSection "\n"}*>>

context-free syntax // sort declarations

  SignatureSection.Sorts = <
    sorts
      <{SortDecl "\n"}*>>
  
  SortDecl.SortDecl = <<ID>>
  SortDecl.InjDecl = [[ID] -> [ID]]
  

context-free syntax // constructor declarations

  SignatureSection.Constructors = <
    constructors
      <{ConsDecl "\n"}*>>

  ConsDecl.ConsDecl = [[ID] : [{Type " * "}*] -> [Type]]

  Type.SimpleSort = <<ID>>
  Type.ListSort = <List(<Type>)> 
  Type = [[MapType]]
  
  MapType.MapSort = [Map<[Type],[Type]>]
  
context-free syntax // semantic component declarations

  SignatureSection.SemanticComponents = <
    semantic-components
      <{SemanticCompDecl "\n"}*>>
  
  SemanticCompDecl.SemanticComponent = [[ID] -> [MapType]]

context-free syntax // internal sort declarations
  
  SignatureSection.InternalSorts = <
    internal-sorts
      <{InternalSortDecl "\n"}*>>
  
  InternalSortDecl.InternalSortDecl = <<ID>>
  InternalSortDecl.InternalInjDecl = [[ID] -> [ID]]

context-free syntax // native data types

  SignatureSection.NativeDataTypes = <
    native-datatypes
      <{NativeTypeDecl "\n"}*>>
  
  JSNIPPET = <<STRING>>
  
  NativeTypeDecl.NativeBaseTypeDecl =
    [[JSNIPPET] as [ID] { [{NativeFunctionDecl "\n"}*] } ]
  
  NativeTypeDecl.NativeSubTypeDecl =
    [[JSNIPPET] as [ID] -> [ID] { [{NativeFunctionDecl "\n"}*] } ]
  
  NativeFunctionDecl.NativeFunctionDecl =
    [[ID] : [{Type " * "}*] -> [Type]] 
  
  NativeFunctionDecl.NativeNoArgFunctionDecl =
    [[ID] : [Type]]

context-free syntax // native operator declarations

  SignatureSection.NativeOperators = <
    native-operators
      <{NativeOpDecl "\n"}*>>
  
  NativeOpDecl.NativeOpDecl = [[ID] : [{Type " * "}*] -> [Type]]

context-free syntax // value term declarations

  SignatureSection.Values = <
    values
      <{ValueDecl "\n"}*>>

  ValueDecl.ValueDecl = [[ID] : [{Type " * "}*]]

context-free syntax // internal constructor declarations

  SignatureSection.InternalConstructors = <
    internal-constructors
      <{InternalConsDecl "\n"}*>>

  InternalConsDecl.InternalConsDecl = [[ID] : [{Type " * "}*] -> [Type]]

context-free syntax // internal constructor declarations

  SignatureSection.NativeConstructors = <
    native-constructors
      <{NativeConsDecl "\n"}*>>

  NativeConsDecl.NativeConsDecl = [[ID] : [{Type " * "}*] -> [Type]]

context-free syntax
  
  SignatureSection.ArrowDeclarations = <
    arrows
      <{ArrowDeclaration "\n"}*>>
  
  ArrowDeclaration.ArrowDecl = [[Type] - [ID] -> [Type]]
  ArrowDeclaration.DefaultArrowDecl = [[Type] --> [Type]]
