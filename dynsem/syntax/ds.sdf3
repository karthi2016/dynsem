module ds

imports Common Module Signatures
 
context-free start-symbols Module

context-free syntax // rules
     
  ModuleSection.Rules = [
    rules 
    
      [ {Rule "\n\n"}* ]
  ] 
  
  Rule.Strict = [strict [ID] [{INT ","}+]/[INT]]
  Rule.Seqstrict = [seqstrict [ID] [{INT ","}+]/[INT]]
    
  Rule.Axiom = [[Formula]]
  
  Rule.Rule = [
    [{Premise ",\n"}+]
    [INFER]
    [Formula]
  ]
  
  Rule.RuleW = [
    [Formula]
    where
      [{Premise ",\n"}+]
  ]
  
  Premise.Formula = [[Formula]]
  Premise.AllFail = <all-fail>

  Premise.MergePoint = 
    [
      [Premise]
      < {
        [{Premise ",\n"}*]
      } + {
        [{Premise ",\n"}*]
      }
    ]
  
  Premise.TryOr = [
  	try {
      [{Premise ",\n"}+]		
  	} or {
  		[{Premise ",\n"}+]
  	}
  ]
  
  ID = <rules> {reject}
  ID = <fresh> {reject}
  
context-free syntax // formulas

  Formula.Match    = [[Term] => [Term]]
  Formula.NMatch   = [[Term] =!=> [Term]] 
  Formula.TermEq   = [[Term] == [Term]]
  Formula.TermNeq  = [[Term] != [Term]]
  
  Formula.Relation = [[Reads][Source] [Rel] [Target]]
  Formula.IsValue  = [value([Term])]
  
  Reads.NoReads = []
  Reads.Reads   = [[{LabelComp ", "}+] |- ]
  
  Source.Source = [[Term]]
  Source.Source = [([Term], [{LabelComp ", "}+])]

  Target.Target = [[Term]]
  Target.Target = [([Term], [{LabelComp ", "}+])]
  
  Rel.NamedDynamic = [- [ID] ->]
  Rel.Dynamic = [-->]
  
  Rel.NamedDynamicEmitted = [- [{LabelComp ", "}+] -[ID]->]
  Rel.DynamicEmitted = [- [{LabelComp ", "}+] ->]
  
  LabelComp.LabelComp = [[Type] [Term]]
  
context-free syntax // terms

  Term.As       = [[ID]@[Term]]

  Term.VarRef   = [[ID]] {avoid}
  // Term.VarRef   = [[LID]] {avoid}

  Term.Var      = [[ID]]
  // Term.Var      = [[LID]]
  Term.Wld      = <_>
  
  Term.String   = [[STRING]]
  Term.Int      = [[INT]]
  Term.Real     = [[REAL]]
  Term.Con      = [[ID]([{Term ", "}*])]
  Term.List     = <[ <{Term ", "}*> ]>
  Term.ListTail = <[ <{Term ", "}*> | <Term> ]>
  
  Term.Fresh  = [fresh]
  Term.True   = [true]
  Term.False  = [false]
  
  Term.Map    = [{[{Entry  ", "}*]}]
  Entry.Bind  = [[Term] |--> [Term]] 
  Entry.Binds = [[Term] |*-> [Term]]
  Entry.MapEntry = [[Term]]
  
  Term.MapSelect = <<Term>[<Term>]>
  Term.MapExtend = [[Term] + [Term]] {assoc}
  Term.MapUnbind = [[Term] \ [Term]] {assoc}

  Term.SortFunCall = <<ID>[<Term>](<{Term ", "}*>)>

context-free priorities

  Term.MapSelect > Term.MapExtend
  
  
  
	