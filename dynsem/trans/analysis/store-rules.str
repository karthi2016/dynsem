module analysis/store-rules

imports
  include/ds
  analysis/lib-analysis
  analysis/constructors
	pp
  ds

rules /* store variables */
  
  store-rules = Rules(map(store-rule))
  
  store-rule:
    r@Rule(p*, _, Relation(Reads(r*), Source(lhs, sc*), NamedDynamicEmitted(_, arrow-name), Target(rhs, tc*))) -> r
    where
      ArrowType(ma-ty, bu-ty) := <lookup-def(|Arrows()); lookup-prop(|Type())> arrow-name;
      <type-of-top> (ma-ty, lhs);
      <map(\ LabelComp(ty, v) -> (<rw-type> ty, v) \; type-of-top)> [r*, sc*];
      <map(type-check-premise-top)> p*;
      <type-of-top> (bu-ty, rhs);
      <map(\ LabelComp(ty, v) -> (<rw-type> ty, v) \; type-of-top)> tc*
  
  store-rule:
    r@Rule(_, _, Relation(_, _, nde@NamedDynamicEmitted(_, arrow-name), _)) -> r
    where
      <not(lookup-def(|Arrows()))> arrow-name;
      add-msg(|Error(), nde, $[Arrow [arrow-name] is not declared])
  
  type-of-top = 
  	type-of
    <+
    where(type-of-failure)

  type-of-failure:
    (ty, t) -> ty
    where
      ty-str := <ensure(pp-type)> ty;
      add-msg(|Error(), t, $[Type check failed: term is incompatible with type [ty-str]]);
      <debug(!"Type check failed, msg created: ")> t

  type-of:
    (ty, Wld()) -> ty
  
  type-of:
    (ty, dec) -> ty
    where
      <?Var(v) + ?MatchedVar(v)> dec
    where
      <store-def(|Vars())> v => v-def;
      <store-prop(|Type(), v-def)> ty
  
  type-of:
    (ty, Int(_)) -> IntType()
    where
      <type-coerce> (IntType(), ty)

  type-of:
    (ty, bool) -> BoolType()
    where
      <?True() + ?False()> bool;
      <type-coerce> (BoolType(), ty)
  
  
  type-of:
    (ty, String(_)) -> StringType()
    where
      <type-coerce> (StringType(), ty)

  type-of:
    (ty, VarRef(v)) -> ty'
    where
      <lookup-def(|Vars()); lookup-prop(|Type())> v => v-ty;
      <type-coerce> (v-ty, ty) => ty'
  
  type-of:
    (ty, ref@VarRef(v)) -> ty
    where
      <not(lookup-def(|Vars()))> v => v-ty;
      add-msg(|Error(), ref, $[Variable reference cannot be resolved])
  
  type-of:
    (ty, Con(c, child*)) -> ty'
    where
      ConstructorType(fchild-ty*, ty-con) := <lookup-def(|Constructors()); lookup-prop(|Type())> c;
      <type-coerce> (ty-con, ty) => ty';
      <zip; map(type-of-top)> (fchild-ty*, child*)

  type-of:
  	(ty, As(v, pat)) -> ty'
  	where
  		<type-of-top> (ty, pat) => ty';
  		<type-of-top> (ty', v)

  type-of:
  	(ty, List([])) -> ty'
  	where
  		<type-coerce> (ListType(ALPHATYPE()), ty) => ty'
  
  type-of:
    (ty, ListTail([h], t)) -> ty'
    where
      <type-of-top> (ty, t) => l-ty@ListType(ALPHATYPE());
      <type-of-top> (NONETYPE(), h) => head-ty;
      <type-coerce> (ListType(head-ty), ty) => ty'
      
  type-of:
    (ty, ListTail([h], t)) -> ty'
    where
      <type-of-top> (ty, t) => l-ty@ListType(elem-ty);
      <not(?ALPHATYPE())> elem-ty;
      <type-of-top> (elem-ty, h) => head-ty;
      <type-coerce> (ListType(head-ty), ty) => ty'

  type-of:
    (ty, SortFunCall(fun-name, par, arg*)) -> ty'
    where
      <type-of-top> (NONETYPE(), par) => parent-ty;
      <lookup-def(|NativeType())> parent-ty => parent-def;
      <lookup-native-fun-def(|fun-name)> parent-def => fun-def;
      <lookup-prop(|Type())> fun-def => FunctionType(farg*, ty');
      <zip; map(type-of-top)> (farg*, arg*)
  
  lookup-native-fun-def(|fun-name):
    parent-def -> <lookup-def(|NativeDTFun(parent-def))> fun-name
  
  lookup-native-fun-def(|fun-name):
    parent-def -> <lookup-native-fun-def(|fun-name)> sup-def
    where
      <not(lookup-def(|NativeDTFun(parent-def)))> fun-name
    where
      <lookup-prop(|SuperType()); lookup-def(|NativeType())> parent-def => sup-def
    
  type-check-premise-top =
  	type-check-premise
    <+
  	where(
  		?p;
  		p-str := <ensure(pp-dynsem-Premise-string)> p;
    	add-msg(|Error(), p, $[Premise type check failed: [p-str]]);
    	<debug(!"Premise check failed: ")> p
  	)

  type-check-premise:
  	f@Formula(TermEq(lhs, rhs)) -> f
  	where
  		<type-of-top> (NONETYPE(), lhs);
  		<type-of-top> (NONETYPE(), rhs)
  
  type-check-premise:
    f@Formula(TermNeq(lhs, rhs)) -> f
    where
      <type-of-top> (NONETYPE(), lhs);
      <type-of-top> (NONETYPE(), rhs)

  type-check-premise:
    f@Formula(Match(lhs, rhs)) -> f
    where
      <type-of-top> (NONETYPE(), lhs) => lhs-ty;
      <type-of-top> (lhs-ty, rhs) => rhs-ty
  
  type-check-premise:
  	f@Formula(Relation(Reads(r*), Source(lhs, sc*), NamedDynamicEmitted(_, arrow-name), Target(rhs, tc*))) -> f
    where
    	ArrowType(bu-ty, ma-ty) := <lookup-def(|Arrows()); lookup-prop(|Type())> arrow-name;
    	<type-of-top> (bu-ty, lhs);
    	<type-of-top> (ma-ty, rhs);
    	<map(\ LabelComp(l, r) -> (<rw-type> l, r) \; type-of-top)> [r*, sc*, tc*]
      
  type-check-premise:
  	f@Formula(NMatch(lhs, rhs)) -> f
  	where
  		<type-of-top> (NONETYPE(), lhs);
  		<type-of-top> (NONETYPE(), rhs)

  type-check-premise:
    f@TypeCast(VarRef(v-name), ty-term) -> f
    where
      <rw-type> ty-term => ty;
      <lookup-def(|Vars())> v-name => v-def;
      <lookup-prop(|Type())> v-def => v-ty;
      if <type-coerce> (ty, v-ty)
      then
        <store-prop(|Type(), v-def)> ty
      else
        add-msg(|Error(), f, $[Type [<pp-type> ty] cannot be coerced to [<pp-type> v-ty]])
      end

  type-check-premise:
  	f@TryOr(PremiseBlock(l*), PremiseBlock(r*)) -> f
    with
      <map(type-check-premise-top)> l*;
      <map(type-check-premise-top)> r*

  type-check-premise:
    f@MergePoint(cond, PremiseBlock(l*), PremiseBlock(r*)) -> f
    with
    	<type-check-premise-top> cond;
      <map(type-check-premise-top)> l*;
      <map(type-check-premise-top)> r*
  
  type-check-premise = ?AllFail()

rules
  
  /**
  * We have a type ty-from that needs to conform to 
  * ty-to
  */
  type-coerce:
    (ty-from, ty-to) -> <fail>
  
  type-coerce = (eq <+ ?(_, NONETYPE())); Fst
  
  type-coerce:
  	(IntType(), RealType()) -> RealType()
  
  type-coerce:
    (ListType(ALPHATYPE()), to-ty@ListType(_)) -> to-ty
  
  type-coerce:
    (ListType(ty-from), ListType(ty-to)) -> <type-coerce> (ty-from, ty-to)
  
  type-coerce:
    (ty-from, ty-to) -> <type-coerce> (sup-ty, ty-to)
    where
      <lookup-def(|Types()); lookup-prop(|SuperType())> ty-from => sup-ty

  type-coerce:
    (ty-from, ty-to) -> <type-coerce> (sup-ty, ty-to)
    where
      <lookup-def(|NativeType()); lookup-prop(|SuperType())> ty-from => sup-ty

  type-coerce-sym = type-coerce <+ !(<Snd>, <Fst>); type-coerce

