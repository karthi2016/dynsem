module analysis/lib-analysis

imports
  include/ds
  analysis/constructors

rules
	
	new-analysis-scope(s) = {| Renames, Declared, Properties: s |}

rules // renaming
  
  new-scope-renames(s|namespace) = {| Renames: s |}
  
  rename(|namespace):
    name -> name'
    where
      <Renames> (namespace, name) => name'
  
  rename(|namespace):
    name -> name'
    where
      <not(Renames)> (namespace, name)
    with
      name' := <newname> name;
      rules(Renames:+ (namespace, name) -> name')

rules // storing and looking up
  
  store-def(|namespace):
    name -> name
    where
      if <not(Declared)> (namespace, name)
      then
        rules(Declared:+ (namespace, name) -> (namespace, name))
      end
  
  lookup-def(|namespace):
    name -> name
    where
      <Declared> (namespace, name)
   
  store-prop(|namespace, name, proptype):
    propval -> propval
    where
      rules(Properties:+ (namespace, proptype, name) -> propval)
   
  lookup-prop(|proptype):
    (namespace, name) -> <Properties> (namespace, proptype, name)

  lookup-def-nss(|xxx) = ![()]
  

