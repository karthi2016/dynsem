module names

imports
  include/ds
  lib/editor-common.generated
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/nabl/-
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/types/-
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/task/-
  lib/runtime-libraries/org.spoofax.meta.runtime.libraries/properties/-
  ds
  names.manual
  	
namespaces
  Module
  Variable
  Arrow
  Sort
  SortFunction
  Constructor

properties
  expected-type of Term : Sort
  sort-kind of Sort : SortKind // Language(), SemanticComponent(), Internal(), NativeType()
  cons-kind of Constructor : ConsKind // Language(), Value(), NativeOperator(), NativeConstructor(), Internal()
  java-id of Sort : JID
  stored-tys of Sort : Term
  
binding rules // module and imports

  Module(name, section*) :
    defines Module name
    scopes Variable, Constructor, Sort, Arrow

  Import(name):
  	imports Sort, Constructor, Arrow from Module name

binding rules

  v@Var(x) :
    defines Variable x of type ty
    where v has expected-type ty
  
  VarRef(x) :
  	refers to Variable x
  	
  Axiom(conc) :
    scopes Variable
 
  Rule(prem*, _, conc) :
  	scopes Variable

  RuleW(conc, prem*) :
  	scopes Variable

binding rules // sorts 

  SimpleSort(ty) :
  	refers to Sort ty

  SortDecl(ty) :
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind Language()
  
  InjDecl(ty, parent) :
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind Language()
    refers to Sort parent
  
  SemanticComponent(ty, MapSort(key-ty, val-ty)):
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind SemanticComponent()
      of stored-tys (key-ty, val-ty)
  
  InternalSortDecl(ty) :
    defines Sort ty
      of sort-kind Internal()

  InternalInjDecl(ty, parent) :
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind Internal()
    refers to Sort parent

binding rules // native datatypes

  NativeBaseTypeDecl(java-id, ty, member*):
    scopes SortFunction
    defines Sort ty
      of type SimpleSort(ty)
      of sort-kind NativeType()
      of java-id java-id

  NativeSubTypeDecl(java-id, ty, supty, member*):
    scopes SortFunction
    imports SortFunction from Sort supty
  	defines Sort ty
  	  of type SimpleSort(ty)
  	  of sort-kind NativeType()
  	  of java-id java-id
  	refers to Sort supty

  NativeFunctionDecl(name, arg*, ty):
    defines SortFunction name
      of type (arg*, ty)
  
binding rules // constructors

  Con(c, t*) :
  	refers to Constructor c

  SortFunCall(f, parent-ref, arg*):
    refers to SortFunction f in Sort parent
      where parent-ref has type SimpleSort(parent)
      and parent has sort-kind NativeType()

  ConsDecl(c, ty*, ty) :
    defines Constructor c
      of type (ty*, ty)
      of cons-kind Language()

  NativeOpDecl(c, ty*, ty):
    defines Constructor c
      of type (ty*, ty)
      of cons-kind NativeOperator()
  
  ValueDecl(c, ty*):
    defines Constructor c
      of type (ty*, SimpleSort("Value"))
      of cons-kind Value()
  
  InternalConsDecl(c, ty*, ty) :
    defines Constructor c
      of type (ty*, ty)
      of cons-kind Internal()
  
  NativeConsDecl(c, ty*, ty) :
    defines Constructor c
      of type (ty*, ty)
      of cons-kind NativeConstructor()

binding rules // arrows

  ArrowDecl(l-ty, name, r-ty):
    defines Arrow name of type (l-ty, r-ty)
  
  NamedDynamic(name):
    refers to Arrow name

  NamedDynamicEmitted(_, name):
    refers to Arrow name

binding rules // variable definitions in matches

  Formula(Match(lhs, MatchedVar(x))):
    defines Variable x
      of type ty
      where lhs has type ty

