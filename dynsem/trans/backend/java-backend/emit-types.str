module backend/java-backend/emit-types

imports
  include/ds

imports
  analysis/lib-analysis
  analysis/constructors

imports
  backend/java-backend/lib-ds2java
  backend/java-backend/utils
  backend/java-backend/analysis-extra
  backend/java-backend/emit-execmethods
  backend/java-backend/emit-arrows

rules /* generate interfaces for sort declarations */
  
  ds2java-abstracts:
    Module(_, section*) -> iface*
    where
      all-type-def*  := <lookup-def-all(|Types())>;
      type-def* := <filter(where(lookup-prop(|SortKind()) => LanguageSort()))> all-type-def*;
      rule* := <fetch-elem(?Rules(<id>))> section*;
      iface* := <map(ds2java-abstracts(|rule*))> type-def*
  
  ds2java-abstracts(|rule*):
    ty-def ->
      compilation-unit |[
        package ~x:<AutoPackageName>;
        
        import org.metaborg.meta.interpreter.framework.*;
        
        public abstract class ~x:<def-get-name; ds2java-sort-classname> ty-def extends AbstractNode implements IMatchableNode {
          ~exec*
        }
      ]|
    where
      exec* := <ds2java-execmethods(ds2java-throw-statement|rule*)> ty-def
 
