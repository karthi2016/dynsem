module backend/java-backend/emit-execmethods

imports
  include/ds
  ds
  libjava-front
  lib-ds
  lib-ds/lib-match

imports
  ds2ds/factorize

imports
  analysis/lib-analysis
  analysis/query
  analysis/constructors
  analysis/analysis-rules
  analysis/mark-references

imports
  backend/java-backend/analysis-extra
  backend/java-backend/lib-ds2java
  backend/java-backend/emit-arrows
  backend/java-backend/utils
  
signature
  constructors
    RuleFail: Statement -> Premise
    JavaStm: List(BlockStm) -> Premise
    Target: ArrowSomething * Term * LabelComp -> Premise
    
rules
  
  ds2java-execmethods(on-norule-generator|rule*):
    c -> <mapconcat(ds2java-execmethod(on-norule-generator|rule-c*))> applicable-arrow*
    where
      ConstructorType(c-c-ty*, c-ty) := <lookup-def(|Constructors()); lookup-prop(|Type())> c;
      applicable-arrow* := <lookup-applicable-arrow-def> c-ty;
      rule-c* := <filter(is-rule-matching-cons(|c, <length> c-c-ty*))> rule*
  
  ds2java-execmethods(on-norule-generator|rule*):
    ty-def -> <mapconcat(ds2java-execmethod(on-norule-generator|rule-l*))> applicable-arrow*
    where
      ty := <def-get-name> ty-def;
      applicable-arrow* := <lookup-applicable-arrow-def> ty;
      rule-l* := <filter(where(get-rule-relation; get-relation-source; ?Cast(_, <id>); rw-type; ?ty))> rule*

  ds2java-execmethods(on-norule-generator|rule*):
    ty@ListType(_) -> <mapconcat(ds2java-execmethod(on-norule-generator|rule-l*))> applicable-arrow*
    where
      applicable-arrow* := <lookup-applicable-arrow-def> ty;
      rule-l* := <filter(where(get-rule-relation; get-relation-source; ?Cast(_, <id>); rw-type; ?ty))> rule*
  
  ds2java-execmethod(on-norule-generator|rule*):
    a@(arrow-def, ArrowType(_, bu-ty)) ->
      class-body-dec* |[
        
        public ~x:<ds2java-returnclassname> (arrow-def, bu-ty) ~x:execname(param*){
          this.specializeChildren(0);
          
          bstm*
        }
        
        ~more-method*
      ]|
    where
      execname := <ds2java-methodname> (arrow-def, bu-ty);
      param* := <lookup-arrow-inputs; map-with-index(ds2java-method-paramdecl)> arrow-def;
      arrow-name := <def-get-name> arrow-def;
      if rule := <fetch-elem(is-rule-matching-arrow(|arrow-name))> rule*
      then
        {| LiftedMethods:
          bstm* := <ds2java-rulestatements(on-norule-generator|a)> rule;
          more-method* := <bagof-LiftedMethods>
        |}
      else
        bstm* := <on-norule-generator> a;
        more-method* := []
      end
  
  ds2java-throw-statement:
    (a-def, ArrowType(_, bu-ty)) -> bstm* |[ throw new InterpreterException("Rule failure", e_aname, this); ]|
    where
      e_aname := Lit(String([Chars(<def-get-name> a-def)]))
  
  ds2java-supercall:
    (arrow-def, ArrowType(_, _)) -> bstm* |[ return super.x_methodname(e*); ]|
    where
      ArrowType(_, bu-ty) := <lookup-prop(|Type())> arrow-def;
      x_methodname := <ds2java-methodname> (arrow-def, bu-ty);
      e* := <lookup-arrow-inputs; map-with-index(\ (idx, _) -> e |[ ~x:$[_[idx]] ]| \)> arrow-def 
  
  ds2java-rulestatements(on-norule-generator|arrow):
    Rule(prem*, _, Relation(Reads(r*), Source(lhs, sc*), NamedDynamicEmitted(_, arrow-name), Target(rhs, tc*))) -> [component*, patternbound*, premstms*]
    where
    	patternbound* := <ds2java-patternbinds> lhs;
    	component* := <map-with-index(ds2java-componentbind)> [r*, sc*];
      lhs-ty := <type-of> lhs;
      retgt := Target(<lookup-arrow-def> (lhs-ty, arrow-name), rhs, tc*);
      prem := <inject-try-or(on-norule-generator|arrow); insert-target(|retgt)> prem*;
      premstms* := <ds2java-premise> (prem, [], [])
  
  ds2java-patternbinds:
  	Con(c, patt-var*) -> <map-with-index(ds2java-patternboundbind)> patt-var*
  
  ds2java-patternbinds:
    As(Var(v), con) -> [asbound*, con-bind*]
    where
    	con-bind* := <ds2java-patternbinds> con;
    	typename := <lookup-def(|Vars()); lookup-prop(|Type()); ds2java-sort-to-classname> v;
      asbound* := bstm* |[ final ~x:typename ~x:v = this; ]|
  
  ds2java-patternbinds:
  	Cast(Var(v), _) -> asbound*
  	where
      typename := <lookup-def(|Vars()); lookup-prop(|Type()); ds2java-sort-to-classname> v;
      asbound* := bstm* |[ final ~x:typename ~x:v = this; ]|
      
  ds2java-componentbind:
  	(idx, LabelComp(_, Var(v))) -> bstm |[ final ~x:typename ~x:v = ~x:$[_[idx]]; ]|
  	where
  		typename := <lookup-def(|Vars()); lookup-prop(|Type()); ds2java-sort-to-classname> v

  ds2java-patternboundbind:
  	(idx, Var(v)) -> bstm |[ final ~x:typename ~x:v = this.~x:$[_[idx]]; ]|
  	where
  		typename := <lookup-def(|Vars()); lookup-prop(|Type()); ds2java-sort-to-classname> v

  inject-try-or(on-norule-generator|arrow):
    p*@[_|_] -> p*
    where
      <not(fetch-elem(premise-can-fail))> p*

  inject-try-or(on-norule-generator|arrow):
    p* -> TryOr(PremiseBlock(p*), PremiseBlock([RuleFail(<on-norule-generator> arrow)]))
    where
      ?[] <+ fetch-elem(premise-can-fail)
  
  premise-can-fail =
    ?Formula(Match(_, Con(_, _)))
    + ?Formula(Match(_, List(_)))
    + ?Formula(Match(_, ListTail(_, _)))
    + ?Formula(NMatch(_, _))
    + ?Formula(TermEq(_, _))
    + ?Formula(TermNeq(_, _))
    + ?MergePoint(_, PremiseBlock(p1*), PremiseBlock(p2*)); <fetch-elem(premise-can-fail)> p2*
    + ?TryOr(_, PremiseBlock(p2*)); <fetch-elem(premise-can-fail)> p2*

  insert-target(|tgt):
    [] -> [tgt]
  
  insert-target(|tgt):
    [px | pxs] -> [px' | <insert-target(|tgt)> pxs]
    where
      px' := <try(insert-target(|tgt))> px 

  insert-target(|tgt):
    MergePoint(p, PremiseBlock(p1*), PremiseBlock(p2*)) -> MergePoint(p, PremiseBlock(p1'*), PremiseBlock(p2'*))
    where
      p1'* := <insert-target(|tgt)> p1*;
      p2'* := <insert-target(|tgt)> p2*

  insert-target(|tgt):
    TryOr(PremiseBlock(p1*), PremiseBlock(p2*)) -> TryOr(PremiseBlock(p1'*), PremiseBlock(p2'*))
    where
      p1'* := <insert-target(|tgt)> p1*;
      p2'* := <insert-target(|tgt)> p2*

  ds2java-premise:
    [] -> []

  ds2java-premise:
    [p | pxs] -> <ds2java-premise> (p, pxs, [])

  ds2java-premise:
    ([p | pxs*], on-succ*, on-fail*) -> <ds2java-premise> (p, [pxs*, on-succ*], on-fail*)

  ds2java-premise:
    (Target((arrow-def, ArrowType(_, bu-ty)), t1, tc*), _, _) ->
      bstm* |[
        return new x_classname(e*);
      ]|
    where
      x_classname := <ds2java-returnclassname> (arrow-def, bu-ty);
      t* := [t1 | <map(?LabelComp(_, <id>))> tc*];
      ty* := [bu-ty | <map(?LabelComp(<id>, _))> tc*];
      e* := <zip(ds2java-term-build)> (ty*, t*)

  ds2java-premise:
    (RuleFail(bstm*), _, _) -> bstm*

  ds2java-premise:
  	(JavaStm(bstm*), _, _) -> bstm*

  ds2java-premise:
    (Formula(Relation(Reads(r*), Source(lhs, sc*), NamedDynamicEmitted(_, arrow-name), Target(vdec@Var(v-name), tc*))), on-succ*, _) ->
      bstm* |[
        final ~x:resultclass x_tmp = e_lhs.~x:exec(e*);
        final ~x:vout-ty ~x:v-name = x_tmp.value;
        bstm1*
        bstm*
      ]|
    where
      (arrow-def, ArrowType(ma-ty, bu-ty)) := <lookup-arrow-def> (<type-of> lhs, arrow-name);
      resultclass := <ds2java-returnclassname> (arrow-def, bu-ty);
      x_tmp := <newname> "$tmp";
      e_lhs := <ds2java-term-build(|ma-ty)> lhs;
      exec := <ds2java-methodname> (arrow-def, bu-ty);
      e* := <map(\ LabelComp(ty, c) -> <ds2java-term-build-optcast(|ty)> c \)> [r*, sc*];
      vout-ty := <type-of; ds2java-sort-to-classname> vdec;
      bstm1* := <map(?LabelComp(_, <id>)); map-with-index(ds2java-bind-out-var(|x_tmp))> tc*;
      bstm* := <ds2java-premise> on-succ*

  ds2java-bind-out-var(|x_result):
    (idx, Var(v)) -> bstm |[ final ~x:v-ty ~x:v = x_result.~x:$[get_[idx]](); ]|
    where
      v-ty := <lookup-def(|Vars()); lookup-prop(|Type()); ds2java-sort-to-classname> v
      
  ds2java-premise:
    (MergePoint(p, PremiseBlock(p1*), PremiseBlock(p2*)), _, _) -> <ds2java-premise> (p, p1*, p2*)

  ds2java-premise:
    (TryOr(PremiseBlock([p1x | p1xs]), PremiseBlock([p2x | p2xs])), _, _) ->
      bstm* |[
        { bstm1* }
        { bstm2* }
      ]|
    where
      bstm1* := <ds2java-premise> (p1x, p1xs, []);
      bstm2* := <ds2java-premise> (p2x, p2xs, [])
  
  ds2java-premise:
    (Formula(NMatch(vin@VarRef(v), pattern)), on-succ*, on-fail*) ->
      bstm* |[
        if(x_antimatch(~x:v)){
          // match failed
          bstm1*
        }else{
          // match succeeded
          bstm2*
        }
      ]|
    where
      x_antimatch := <newname> "$antimatch";
      x_inputtmp := <newname> "$tmp";
      vin-ty := <type-of> vin;
      <store-prop(|Type(), <store-def(|Vars())> x_inputtmp)> vin-ty; 
      prem* := <factorize-premises; map(mark-vardefs-in-premise); map(mark-match-vars); map(type-check-premise-top)> [Formula(Match(VarRef(x_inputtmp), pattern))];
      bstm0* := <ds2java-premise> (prem*, [JavaStm(bstm* |[ return false; ]|) ], []);
      rules(
        LiftedMethods:+ _ ->
          class-body-dec |[
            private boolean x_antimatch(~x:<ds2java-sort-to-classname> vin-ty x_inputtmp) {
              bstm0*
              return true;
            }
          ]|
      );
      bstm1* := <ds2java-premise> on-succ*;
      bstm2* := <ds2java-premise> on-fail*
  
  ds2java-premise:
    (Formula(Match(lhs@Con(c, _), dec@MatchedVar(v))), on-succ*, _) ->
      bstm* |[
        final ~x:<ds2java-constr-classname> c ~x:v = e_lhs;
        bstm*
      ]|
    where
    	c-kind := <lookup-def(|Constructors()); lookup-prop(|ConsKind())> c;
    	<not(?NativeOpCons())> c-kind
    where
      v-ty := <type-of> dec;
      e_lhs := <ds2java-term-build-optcast(|v-ty)> lhs;
      bstm* := <ds2java-premise> on-succ*
  
  ds2java-premise:
    (Formula(Match(lhs, dec@MatchedVar(v))), on-succ*, _) ->
      bstm* |[
        final ~x:<ds2java-sort-to-classname> v-ty ~x:v = e_lhs;
        bstm*
      ]|
    where
      v-ty := <type-of> dec;
      e_lhs := <ds2java-term-build-optcast(|v-ty)> lhs;
      bstm* := <ds2java-premise> on-succ*
  
  ds2java-premise:
    (Formula(Match(VarRef(v), Con(c, patt-var*))), on-succ*, on-fail*) ->
      bstm* |[
        final ~x:consname x_tmp = ~x:v.match(~x:consname.class);
        if(x_tmp != null){
          bstm1* // bind pattern variables
          bstm2* // on-success
        } else {
          bstm3* // on failure
        }
      ]|
    where
      consname := <ds2java-constr-classname> c;
      x_tmp := <newname> "$tmp";
      bstm1* := <map-with-index(ds2java-bind-out-var(|x_tmp))> patt-var*;
      bstm2* := <ds2java-premise> on-succ*;
      bstm3* := <ds2java-premise> on-fail*
  
  ds2java-premise:
    (Formula(Match(VarRef(x_v), ListTail([hd@Var(x_hv)], tl@Var(x_tv)))), on-succ*, on-fail*) ->
      bstm* |[
        if(x_v != null && !x_v.isEmpty()){
          final ~x:hd-ty x_hv = x_v.head();
          final ~x:tl-ty x_tv = x_v.tail();
          bstm1*
        }else{
          bstm2*
        }
      ]|
    where
      hd-ty := <type-of; ds2java-sort-to-classname> hd;
      tl-ty := <type-of; ds2java-sort-to-classname> tl;
      bstm1* := <ds2java-premise> on-succ*;
      bstm2* := <ds2java-premise> on-fail*

  ds2java-premise:
    (Formula(TermNeq(t1, t2)), on-succ*, on-fail*) -> <ds2java-premise> (Formula(TermEq(t1, t2)), on-fail*, on-succ*)
  
  ds2java-premise:
    (Formula(TermEq(t1, t2)), on-succ*, on-fail*) ->
      bstm* |[
        if(e_t1 == e_t2){
          bstm1*
        } else {
          bstm2*
        }
      ]|
    where
      <type-of; ds2java-type-is-primitive> t1;
      <type-of; ds2java-type-is-primitive> t2;
      e_t1 := <ds2java-term-build(|ALPHATYPE())> t1;
      e_t2 := <ds2java-term-build(|ALPHATYPE())> t2;
      bstm1* := <ds2java-premise> on-succ*;
      bstm2* := <ds2java-premise> on-fail*
  
  ds2java-premise:
    (Formula(TermEq(t1, t2)), on-succ*, on-fail*) ->
      bstm* |[
        if(e_t1 != null && e_t1.equals(e_t2)){
          bstm1*
        } else {
          bstm2*
        }
      ]|
    where
      <type-of; not(ds2java-type-is-primitive)> t1;
      <type-of; not(ds2java-type-is-primitive)> t2
    where
      e_t1 := <ds2java-term-build(|ALPHATYPE())> t1;
      e_t2 := <ds2java-term-build(|ALPHATYPE())> t2;
      bstm1* := <ds2java-premise> on-succ*;
      bstm2* := <ds2java-premise> on-fail*

  ds2java-premise:
    (Formula(TypeCheck(VarRef(v), ty-trm)), on-succ*, on-fail*) ->
      bstm* |[
        if(~x:v instanceof ~x:ty){
          bstm1*
        }else{
          bstm2*
        }
      ]|
    where
      ty := <rw-type; ds2java-sort-to-classname> ty-trm;
      bstm1* := <ds2java-premise> on-succ*;
      bstm2* := <ds2java-premise> on-fail*

  ds2java-premise = debug(!"Premise failure: "); fail
  
  ds2java-term-build-optcast(|ex_ty):
    trm -> <ds2java-term-build(|ex_ty)> trm
    where not(
      <type-of> trm => MapType(ALPHATYPE(), ALPHATYPE())
    )
  
  ds2java-term-build-optcast(|ex_ty):
    trm -> <ds2java-term-build(|ex_ty)> Cast(trm, ex_ty)
    where
      <type-of> trm => MapType(ALPHATYPE(), ALPHATYPE())

  ds2java-term-build:
    (ty, t) -> <ds2java-term-build(|ty)> t
  
  ds2java-term-build(|ex_ty):
    Int(i) -> e |[ i ]|

  ds2java-term-build(|ex_ty):
    True() -> e |[ true ]|
  
  ds2java-term-build(|ex_ty):
    False() -> e |[ false ]|
  
  ds2java-term-build(|ex_ty):
    VarRef(x_vref) -> e |[ x_vref ]|
  
  ds2java-term-build(|ex_ty):
    Con(c, child*) -> e |[ new x_consclass(this.getSourceInfo(), e*) ]|
    where
      c-def := <lookup-def(|Constructors())> c;
      c-kind := <lookup-prop(|ConsKind())> c-def;
      <not(?NativeOpCons())> c-kind
    where
      x_consclass := <ds2java-constr-classname> c;
      ConstructorType(c-c-ty*, _) := <lookup-prop(|Type())> c-def;
      e* := <zip(\ (ex_ty', trm) -> <ds2java-term-build(|ex_ty')> trm \)> (c-c-ty*, child*)

  ds2java-term-build(|ex_ty):
    Con(c, child*) -> e |[ x_manualpkg.Natives.x_consname(e*) ]|
    where
    	c-def := <lookup-def(|Constructors())> c;
      <lookup-prop(|ConsKind())> c-def => NativeOpCons();
      ConstructorType(c-c-ty*, _) := <lookup-prop(|Type())> c-def;
      x_manualpkg := <ManuPackageName>;
      x_consname := $[[c]_[<length> c-c-ty*]];
      e* := <zip(\ (ex_ty', trm) -> <ds2java-term-build(|ex_ty')> trm \)> (c-c-ty*, child*)
  
  ds2java-term-build(|ex_ty):
    SortFunCall(x_fname, par, arg*) -> e |[ e_on.x_fname(e*)]|
    where
    	e_on := <ds2java-term-build(|ALPHATYPE())> par;
    	par-ty := <type-of> par;
      par-def := <lookup-def(|Types())> par-ty;
      fun-def := <lookup-native-fun-def(|x_fname)> par-def;
      <lookup-prop(|Type())> fun-def => FunctionType(farg*, _);
    	e* := <zip(\ (ex_ty', trm) -> <ds2java-term-build(|ex_ty')> trm \)> (farg*, arg*)
  
  ds2java-term-build(|ex_ty):
  	List([]) -> e |[ new x_type(this.getSourceInfo()) ]|
  	where
  		if <?ListType(_)> ex_ty
  		then
  			x_type := <ds2java-sort-to-classname> ex_ty
  		else
  			x_type := <ds2java-sort-to-classname> ListType(ex_ty)
  		end 
  
  ds2java-term-build(|ex-ty):
    ListTail([h-trm], t-trm) -> e |[ new x_type(this.getSourceInfo(), e_h, e_t) ]|
    where
      h-ty := <type-of> h-trm;
      e_h := <ds2java-term-build(|h-ty)> h-trm;
      h-ex-elem-ty := <?ListType(<id>) <+ !h-ty> ex-ty;
      if <type-of> t-trm => ListType(t-elem-ty)
      then
        actual_ex_elem_ty := <type-coerce-sym(type-coerce-direct(fail))> (h-ex-elem-ty, t-elem-ty)
      else
        actual_ex_elem_ty := h-ex-elem-ty
      end;
      x_type := <ds2java-sort-to-classname> ListType(actual_ex_elem_ty);
      e_t := <ds2java-term-build(|ListType(actual_ex_elem_ty))> t-trm
  
  ds2java-term-build(|ex_ty):
    Cast(t, ty-trm) -> e |[ (x_ty) e_t ]|
    where
      e_t := <ds2java-term-build(|<try(rw-type)> ty-trm)> t;
      x_ty := <try(rw-type); ds2java-sort-to-classname> ty-trm
  
  ds2java-term-build(|ex_ty):
    String(s) -> Lit(String([Chars(<unquote(?'"')> s)]))

  ds2java-term-build(|ex_ty):
    Fresh() -> e |[ x_manualpkg.Natives.fresh() ]|
    where
      x_manualpkg := <ManuPackageName>
  
  ds2java-term-build(|ex_ty):
    MapSelect(map-expr, key-expr) -> e |[ e_map.get(e_key) ]|
    where
      e_map := <ds2java-term-build(|ALPHATYPE())> map-expr;
      e_key := <ds2java-term-build(|ALPHATYPE())> key-expr

  ds2java-term-build(|ex_ty):
    MapExtend(lmap, rmap) -> e |[ MapUtils.plus(e_rmap, e_lmap) ]|
    where
      e_rmap := <ds2java-term-build(|ALPHATYPE())> rmap;
      e_lmap := <ds2java-term-build(|ALPHATYPE())> lmap

  ds2java-term-build(|ex_ty):
    Map([Bind(key, val)]) -> e |[ new com.github.krukow.clj_lang.PersistentTreeMap<x_keyty, x_valty>().plus(e_key, e_val) ]|
    where
      x_keyty := <type-of; ds2java-sort-to-classname; box-primitive-type> key;
      x_valty := <type-of; ds2java-sort-to-classname; box-primitive-type> val;
      e_key := <ds2java-term-build(|ALPHATYPE())> key;
      e_val := <ds2java-term-build(|ALPHATYPE())> val

  ds2java-term-build(|ex_ty):
    Map([]) -> e |[ PersistentTreeMap.EMPTY ]|
      
  ds2java-term-build(|ex_ty) = debug(!"Term build failure: "); !e |[ xx ]|
  
