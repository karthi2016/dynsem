module backend/interpreter/main

imports
  include/ds
  editor-common.generated
  lib-ds
  ds
  libstratego-aterm

imports
  backend/interpreter/-
  ds2ds/-
  analysis/-
  
rules

  ds-to-interp-editor:
    (_, position, ast, path, project-path) -> None()
    with
      result := <ds-to-interp-main(|path, project-path)> ast;
      <ds-to-interp-terms(|path, project-path)> result;
      fout := <fopen> (<SpecTargetFile>, "w");
      <pp-aterm-stream(|fout)> result;
      <fclose> fout;
      <file-copy> (<TableSourceFile>, <TableTargetFile>)
      
  
  ds-to-interp-main(|path, project-path):
    ast -> result
    with
      in-project-path(
        ds-to-interp-init-options(|path, project-path);
        <log-timed(
          log-timed(
            include-imports | "Import flattening"
          );
          log-timed(
            desugar-top; fuse-sections | "Desugaring"
          );
          log-timed(
            desugar-varschemes-module | "Desugaring variable schemes"
          );
          log-timed(
            add-extra-typeannos-module | "Adding extra typeannos"
          );
          log-timed(
            desugar-aliases-module | "Desugaring aliases"
          );
          log-timed(
            factorize-module | "Factorization 1/2"
          );
          log-timed(
            expand-implicits-module | "Implicits expansion"
          );
          log-timed(
            factorize-module | "Factorization 2/2"
          );
          log-timed(
            explicate-module | "Explication"
          );
          log-timed(
            copy-propagation-module | "Copy propagation"
          );
          log-timed(
            constant-propagation-module | "Constant propagation"
          );
          log-timed(
            mark-rule-arguments-module | "Marking arguments"
          );
          log-timed(
            desugar-natives-module | "Desugaring native ops"
          )
        | "Total")> ast => result
      |project-path)
  
  ds-to-interp-terms(|path, project-path) =
    in-project-path(
        ds-to-interp-init-options(|path, project-path);
        log-timed(
          ds-to-interp-terms-module | "Language-specific terms"
        )
    |project-path)

    
    
    
    