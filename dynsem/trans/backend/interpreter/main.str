module backend/interpreter/main

imports
  signatures/ds-sig
  stratego/metaborg
  lib-ds
  ds
  libstratego-aterm

imports
  backend/interpreter/-
  ds2ds/-
  analysis/-
  
rules

  ds-to-interp-editor:
    (_, position, ast, path, project-path) -> None()
    with
      local-project-path := $[[<prim("local_path")> project-path]/];
      result := <ds-to-interp-main(|path, local-project-path)> ast;
      <ds-to-interp-terms(|path, local-project-path)> result;
      fout := <debug(!"fopen: "); fopen> (<SpecTargetFile>, "w");
      <pp-aterm-stream(|fout)> result;
      <fclose> fout;
      <copy-file> (<TableSourceFile>, <TableTargetFile>)
  
  ds-to-interp-main(|path, project-path) =
    in-project-path(
        ds-to-interp-init-options(|path, project-path)
        ; include-imports
        ; desugar-top
        ; fuse-sections
        ; desugar-varschemes-module
        ; add-extra-typeannos-module
        ; desugar-aliases-module
        ; factorize-module
        ; expand-implicits-module
        ; factorize-module
        ; explicate-module
        ; copy-propagation-module
        ; constant-propagation-module
        ; defactorize-module
        ; desugar-for-interpreter-module
    |project-path)
  
  ds-to-interp-terms(|path, project-path) =
    in-project-path(
        ds-to-interp-init-options(|path, project-path);
        log-timed(
          ds-to-interp-terms-module | "Language-specific terms"
        )
    |project-path)

    
    
    
    