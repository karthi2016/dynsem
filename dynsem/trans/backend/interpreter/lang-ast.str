module backend/interpreter/lang-ast

imports
  include/ds
  backend/interpreter/utils
  libjava-front
  editor-common.generated

rules
  
  ds-to-interp-terms-module =
    ?Module(_, <fetch-elem(?Signatures(sig*))>);
    debug(!"generating term classes");
    <mapconcat(debug(!"ba1a "); ds-to-interp-terms-signatures; debug(!"ba1b ") <+ ds-to-interp-terms-report-unsupported); ds-to-interp-write-classes(|<TermsPackageName>)> sig*;
    debug(!"generating typesystem class");
    <ds-to-interp-terms-types-signatures; MkSingleton; ds-to-interp-write-classes(|<TopPackageName>)> sig*;
    debug(!"generating termbuilding sort-classes");
    <ds-to-inter-terms-sortbuilding; ds-to-interp-write-classes(|<TermBuildPackageName>)> sig*;
    debug(!"generating termbuilding classes");
    <ds-to-interp-terms-building; ds-to-interp-write-classes(|<TermBuildPackageName>)> sig*;
    debug(!"generating termmatching classes");
    <ds-to-interp-terms-matching; ds-to-interp-write-classes(|<TermMatchPackageName>)> sig*;
    debug(!"generating term registry class");
    <fetch-elem(?Constructors(<id>)); ds-to-interp-terms-registry; MkSingleton; ds-to-interp-write-classes(|<TopPackageName>)> sig*;
    debug(!"done generating classes")

rules /* Sort & Constructor classes */

  ds-to-interp-terms-signatures = 
    ?Sorts(<id>);
    map(ds-to-interp-terms-sortdecl)

  ds-to-interp-terms-sortdecl:
    dec@SortDecl(_) ->
      compilation-unit |[
        package ~x:<TermsPackageName>;
        
        import metaborg.meta.lang.dynsem.interpreter.terms.IConTerm;
        
        public interface ~x:<ds-to-interp-terms-types-name> dec extends IConTerm {
        
        }
      ]|

  ds-to-interp-terms-signatures =
    debug(!"ba1a1a ");
    ?Constructors(<id>);
    debug(!"ba1a1b ");    
    map(debug(!"ba1a1c "); ds-to-interp-terms-consdecl; debug(!"ba1a1d "))
    ;debug(!"ba1a1e ")
  
  ds-to-interp-terms-consdecl:
    dec@ConsDecl(name, kid*, s, _) ->
      compilation-unit |[
        package ~x:<debug(!"ba1a1c1 "); TermsPackageName; debug(!"ba1a1c2 ")>;
        
        public class x_classname implements ~x:<ds-to-interp-terms-types-name> s {

          public final static String CONSTRUCTOR = ~e:Lit(String([Chars(name)]));
          public final static int ARITY = ~i:<length; int-to-string> kid*;

          ~fdec0*

          ~fget0*

          public x_classname(param0*) {
            bstm0*
          }
          
          @Override
          public String constructor() {
            return CONSTRUCTOR;
          }
        
          @Override
          public int arity() {
            return ARITY;
          }
          
          @Override
          public Object[] allSubterms() {
            return new Object[] { e0* };
          }
        }
      ]|
    where
      x_classname := <ds-to-interp-terms-types-name> dec;
      param0* := <ds-to-interp-sorts-to-params> kid*;
      bstm0* := <ds-to-interp-sorts-to-finits> kid*;
      fdec0* := <ds-to-interp-sorts-to-fdecs> kid*;
      fget0* := <ds-to-interp-sorts-to-fgets> kid*;
      e0* := <ds-to-interp-sorts-to-frefs> kid*
  
  ds-to-interp-sorts-to-params = is-list; map-with-index(ds-to-interp-sort-to-param)
  
  ds-to-interp-sort-to-param:
    (i, s) -> param |[ x_sortclass x_name ]|
    where
      x_sortclass := <ds-to-interp-terms-types-name> s;
      x_name := $[_[i]]
  
  ds-to-interp-sorts-to-finits = is-list; map-with-index(ds-to-interp-sort-to-finit)
  
  ds-to-interp-sort-to-finit:
    (i, _) -> bstm |[ this.x_name = x_name; ]|
    where
      x_name := $[_[i]]

  ds-to-interp-sorts-to-fdecs = is-list; map-with-index(ds-to-interp-sort-to-fdec)
  
  ds-to-interp-sort-to-fdec:
    (i, s) -> class-body-dec |[ private final x_sortclass x_name; ]|
    where
      x_sortclass := <ds-to-interp-terms-types-name> s;
      x_name := $[_[i]]

  ds-to-interp-sorts-to-fgets = is-list; map-with-index(ds-to-interp-sort-to-fget)
  
  ds-to-interp-sort-to-fget:
    (i, s) ->
      class-body-dec |[
        public x_sortclass x_getname() {
          return x_name;
        }
      ]|
    where
      x_sortclass := <ds-to-interp-terms-types-name> s;
      x_getname := $[get_[i]];
      x_name := $[_[i]]

  ds-to-interp-sorts-to-frefs = is-list; map-with-index(ds-to-interp-sort-to-fref)
  
  ds-to-interp-sort-to-fref:
    (i, _) -> e |[ x_name ]|
    where
      x_name := $[_[i]]

rules /* type system class */

  ds-to-interp-terms-types-signatures:
    sig-sec* ->
      compilation-unit |[
        package ~x:<TopPackageName>;

        import com.oracle.truffle.api.dsl.TypeSystem;
        import ~x:$[[<TermsPackageName>].*];
        @TypeSystem({ ~type* })
        public class Types {
          
        }
      ]|
    where 
      filter(?Sorts(<id>) + ?Constructors(<id>));
      concat;
      qsort(?(ConsDecl(_, _, _, _), SortDecl(_)));
      map(ds-to-interp-terms-types-name);
      map(!Lit(Class(ClassOrInterfaceType(TypeName(Id(<id>)), None())))) => type*

rules /* term building */

  ds-to-inter-terms-sortbuilding = is-list; fetch-elem(?Sorts(<id>)); map(ds-to-interp-terms-sortbuilding)
  
  ds-to-interp-terms-sortbuilding:
    dec@SortDecl(s) -> 
      compilation-unit |[
        package ~x:<TermBuildPackageName>;
  
        import ~x:$[[<TermsPackageName>].[<ds-to-interp-terms-types-name> dec]];
        import ~x:$[[<TopPackageName>].TypesGen];
        
        import org.metaborg.meta.lang.dynsem.interpreter.nodes.building.TermBuild;
        import com.oracle.truffle.api.frame.VirtualFrame;
        import com.oracle.truffle.api.nodes.UnexpectedResultException;
        import com.oracle.truffle.api.source.SourceSection;
        
        public abstract class x_class extends TermBuild {
        
          public x_class(SourceSection source) {
            super(source);
          }
        
          public x_sortclass x_execmethod(VirtualFrame frame)
              throws UnexpectedResultException {
            return TypesGen.x_expectmethod(executeGeneric(frame));
          }
        
        }
      ]|
    where
      x_class := $[A[<ds-to-interp-terms-types-name> dec]Build];
      x_sortclass := <ds-to-interp-terms-types-name> dec;
      x_execmethod := $[execute[x_sortclass]];
      x_expectmethod := $[expect[x_sortclass]]

  ds-to-interp-terms-building = is-list; fetch-elem(?Constructors(<id>)); map(ds-to-interp-terms-building)
  
  ds-to-interp-terms-building:
    dec@ConsDecl(_, kid*, s, _) ->
      compilation-unit |[
        package ~x:<TermBuildPackageName>;
      
        import ~x:$[[<TermsPackageName>].*];
        import org.metaborg.meta.lang.dynsem.interpreter.nodes.building.*;
        
        import com.oracle.truffle.api.dsl.NodeChild;
        import com.oracle.truffle.api.dsl.NodeChildren;
        import com.oracle.truffle.api.dsl.Specialization;
        import com.oracle.truffle.api.source.SourceSection;

        @NodeChildren({ ~child* })
        public abstract class x_class extends ~x:$[A[<ds-to-interp-terms-types-name> s]Build] {

          public x_class(SourceSection source) {
            super(source);
          }
          
          @Specialization
          public x_termclass doTyped(param0*) {
            return new x_termclass(e0*);
          }
          
        }
      ]|
    where
      x_class := $[[<ds-to-interp-terms-types-name> dec]Build];
      child* := <ds-to-interp-terms-building-childannos> kid*;
      x_termclass := <ds-to-interp-terms-types-name> dec;
      param0* := <ds-to-interp-terms-building-params> kid*;
      e0* := <ds-to-interp-terms-building-exprs> kid*

  ds-to-interp-terms-building-childannos = map-with-index(ds-to-interp-terms-building-childanno)
  
  ds-to-interp-terms-building-childanno:
    (i, s) -> anno |[ @NodeChild(value = ~e:Lit(String([Chars($[tb_[i]])])), type = x_childterm.class) ]|
    where
      x_childterm := <ds-to-interp-terms-building-absbuildname> s
  
  ds-to-interp-terms-building-absbuildname:
    sref@SimpleSort(s) -> $[A[<ds-to-interp-terms-types-name> sref]Build]
    where
      <not(is-builtin-type)> s

  ds-to-interp-terms-building-absbuildname:
    SimpleSort("String") -> "LiteralTermBuild.StringLiteralTermBuild"
  
  ds-to-interp-terms-building-absbuildname:
    SimpleSort("Int") -> "LiteralTermBuild.IntLiteralTermBuild"

  ds-to-interp-terms-building-params = map-with-index(ds-to-interp-terms-building-param)
  
  ds-to-interp-terms-building-param:
    (i, s) -> param |[ final ~x:<ds-to-interp-terms-types-name> s ~x:$[tb_[i]] ]|

  ds-to-interp-terms-building-exprs = map-with-index(ds-to-interp-terms-building-expr)
  
  ds-to-interp-terms-building-expr:
    (i, _) -> e |[ ~x:$[tb_[i]] ]|

rules /* term matching */

  ds-to-interp-terms-matching = is-list; fetch-elem(?Constructors(<id>)); map(ds-to-interp-terms-matching)
  
  ds-to-interp-terms-matching:
    dec@ConsDecl(_, kid*, _, _) -> 
      compilation-unit |[
        package ~x:<TermMatchPackageName>;
        
        import ~x:<TermsPackageName>.x_termclass;
        import org.metaborg.meta.lang.dynsem.interpreter.nodes.matching.MatchPattern;
        
        import com.oracle.truffle.api.dsl.Specialization;
        import com.oracle.truffle.api.frame.VirtualFrame;
        import com.oracle.truffle.api.source.SourceSection;
        
        public abstract class x_matcherclass extends MatchPattern {
        
          ~fdec*
        
          public x_matcherclass(SourceSection source, param0*) {
            super(source);
            bstm0*
          }
        
          @Specialization
          public boolean execute(x_termclass term, VirtualFrame frame) {
            return e1;
          }
        }
      ]|
    where
      x_termclass := <ds-to-interp-terms-types-name> dec;
      x_matcherclass := $[[x_termclass]MatchPattern];
      fdec* := <ds-to-interp-terms-matching-fdecs> kid*; 
      param0* := <ds-to-interp-terms-matching-initparams> kid*;
      bstm0* := <ds-to-interp-terms-matching-finits> kid*;
      e1 := <foldr(!e |[ true ]|, ds-to-interp-terms-matching-matchdescend)> <range; reverse> (1, <length; inc> kid*)
      
  ds-to-interp-terms-matching-initparams = map-with-index(ds-to-interp-terms-matching-initparam)
  
  ds-to-interp-terms-matching-initparam:
    (i, s) -> param |[ MatchPattern ~x:$[p_[i]] ]|

  ds-to-interp-terms-matching-fdecs = map-with-index(ds-to-interp-terms-matching-fdec)
  
  ds-to-interp-terms-matching-fdec:
    (i, _) -> class-body-dec |[ @Child protected MatchPattern ~x:$[p_[i]]; ]|

  ds-to-interp-terms-matching-finits = map-with-index(ds-to-interp-terms-matching-finit)
  
  ds-to-interp-terms-matching-finit:
    (i, s) -> bstm |[ this.~x:$[p_[i]] = ~x:$[p_[i]]; ]|

  ds-to-interp-terms-matching-matchdescend:
    (i, e) -> e |[ e && ~x:$[p_[i]].execute(term.~x:$[get_[i]](), frame) ]|

rules /* term registry */


  ds-to-interp-terms-registry:
    consdecl* ->
      compilation-unit |[
        package ~x:<TopPackageName>;
        
        import java.util.HashMap;
        import java.util.Map;
        
        import ~x:<TermBuildPackageName>.*;
        import ~x:<TermMatchPackageName>.*;
        
        import org.metaborg.meta.lang.dynsem.interpreter.ITermRegistry;
        import org.metaborg.meta.lang.dynsem.interpreter.nodes.building.*;
        import org.metaborg.meta.lang.dynsem.interpreter.nodes.matching.*;
        
        import com.oracle.truffle.api.source.SourceSection;
        
        public class TermRegistry implements ITermRegistry {
        
          private final Map<String, ITermBuildFactory> builderFactories = new HashMap<String, ITermBuildFactory>();
          private final Map<String, ITermMatchPatternFactory> matcherFactories = new HashMap<String, ITermMatchPatternFactory>();
        
          public TermRegistry() {
            init();
          }
        
          private void init() {
            bstm0*
          }
        
          @Override
          public ITermBuildFactory lookupBuildFactory(String constr, int arity) {
            return builderFactories.get(constr + "/" + arity);
          }
        
          @Override
          public ITermMatchPatternFactory lookupMatchFactory(String constr, int arity) {
            return matcherFactories.get(constr + "/" + arity);
          }
        }
      ]|
    where
      bstm0* := <ds-to-interp-terms-registry-inits> consdecl* 


  ds-to-interp-terms-registry-inits = mapconcat(ds-to-interp-terms-registry-init)
  
  ds-to-interp-terms-registry-init:
    dec@ConsDecl(name, kid*, _, _) ->
      bstm* |[
       builderFactories.put(e_conkey, new ITermBuildFactory() {
    
          @Override
          public x_termbuildclass apply(SourceSection source,
              TermBuild... children) {
            return x_termbuildNODEGEN.create(source, e1*);
          }
        });

        matcherFactories.put(e_conkey, new ITermMatchPatternFactory() {
    
          @Override
          public MatchPattern apply(SourceSection source,
              MatchPattern... children) {
            return x_termmatchNODEGEN.create(source, e2*);
          }
        });
      ]|
    with
      e_conkey := Lit(String([Chars($[[name]/[<length> kid*]])]));
      x_termbuildclass := $[[<ds-to-interp-terms-types-name> dec]Build];
      x_termbuildNODEGEN := $[[x_termbuildclass]NodeGen];
      x_termclass := <ds-to-interp-terms-types-name> dec;
      x_termmatchNODEGEN := $[[x_termclass]MatchPatternNodeGen];
      e1* := <map-with-index(\ (i, s) -> e |[ (~x:<ds-to-interp-terms-building-absbuildname> s) children[~i:<dec; int-to-string> i] ]| \)> kid*;
      e2* := <map-with-index(\ (i, _) -> e |[ children[~i:<dec; int-to-string> i] ]| \)> kid*

rules /* utils */

  ds-to-interp-terms-types-name:
    SortDecl(s) -> $[I[s]Term]

  ds-to-interp-terms-types-name:
    SimpleSort(s) -> $[I[s]Term]
    where
      <not(is-builtin-type)> s
  
  ds-to-interp-terms-types-name:
    SimpleSort("String") -> $[String]

  ds-to-interp-terms-types-name:
    SimpleSort("Int") -> $[Int]
  
  is-builtin-type = ?"String" + ?"Int"
  
  ds-to-interp-terms-types-name:
    ConsDecl(name, child*, _, _) -> $[[name]_[<length> child*]_Term]

  ds-to-interp-terms-report-unsupported = debug(!"Unsupported signature section "); ![]

