module backend/interpreter/rule-arguments

imports
  include/ds
  ds
  backend/interpreter/signature

rules

  mark-rule-arguments-module:
    m@Module(_, _) -> Module($[[name]_markedargs], section*)
    where
      <mark-rule-arguments> m => Module(name, section*)
      
  mark-rule-arguments = alltd(?Rule(_, _, _); mark-rule-arguments-rule)
  
  mark-rule-arguments-rule:
    Rule(prem*, infer, Relation(ro, Source(lhs, rw), arrow, target))
      -> Rule(prem'*, infer, Relation(ro', Source(lhs', rw'), arrow, target'))
    with {| RenameArgument, NextArgument:
      rules(NextArgument: _ -> 1);
      lhs' := <mark-rule-arguments-bind-source> lhs;
      ro' := <alltd(mark-rule-arguments-bind)> ro;
      rw' := <alltd(mark-rule-arguments-bind)> rw;
      prem'* := <alltd(mark-rule-arguments-read)> prem*;
      target' := <alltd(mark-rule-arguments-read)> target
    |}

  mark-rule-arguments-bind-source = not(?As(_, _)); alltd(mark-rule-arguments-bind)

  mark-rule-arguments-bind-source:
    As(VarRef(v), source) -> <alltd(mark-rule-arguments-bind)> source
    where rules( RenameArgument: VarRef(v) -> ArgRead(0) )

  mark-rule-arguments-bind:
    VarRef(v) -> ArgBind(i)
    with
      i := <NextArgument>;
      rules(
        RenameArgument: VarRef(v) -> ArgRead(i)
        NextArgument: _ -> <inc> i
      )

  mark-rule-arguments-read = RenameArgument
