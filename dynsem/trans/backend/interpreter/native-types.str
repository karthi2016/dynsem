module backend/interpreter/native-types

imports
  include/ds
  ds
  backend/interpreter/signature

rules

  desugar-native-types-module:
    m@Module(_, _) -> Module($[[name]_natops], section*)
    where
      <m-in-analysis(desugar-native-types); unmark-vars> m => Module(name, section*)
  
  desugar-native-types = alltd(desugar-native-types-sortref + desguar-native-types-sortfuncall)
  
  desugar-native-types-sortref:
    SimpleSort(s) -> NativeType(s, ty-str)
    where
      ty-def := <lookup-def(|Types())> s;
      NativeSort() := <lookup-prop(|SortKind())> ty-def;
      ty-str := <lookup-prop(|NativeTypeJString()); unquote(?'"')> ty-def

  desguar-native-types-sortfuncall:
    SortFunCall(name, recv, arg*) -> NativeFunCall(<type-of> recv, name, recv, arg*)

