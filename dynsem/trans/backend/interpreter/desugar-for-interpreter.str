module backend/interpreter/desugar-for-interpreter

imports
  stratego/metaborg
  signatures/-
  backend/interpreter/signature
  analysis/-

rules

  desugar-for-interpreter-module:
    m@Module(_, _) -> Module($[[name]_interp], section*)
    where
      <m-in-analysis(desugar-for-interpreter); unmark-vars> m => Module(name, section*)

  desugar-for-interpreter =
    desugar-semantic-components;
    mark-rule-arguments;
    desugar-natives;
    desugar-list-reductions;
    desugar-rec-tco

rules /* rename arguments */

  mark-rule-arguments = alltd(?Rule(_, _, _); mark-rule-arguments-rule)
  
  mark-rule-arguments-rule:
    Rule(prem*, infer, Relation(Source(lhs, rw*), arrow, target))
      -> Rule(prem'*, infer, Relation(Source(lhs', rw*), arrow, target'))
    with {| RenameArgument, NextArgument:
      rules(NextArgument: _ -> 1);
      lhs' := <try(mark-rule-arguments-bind-source)> lhs;
      prem'* := <alltd(mark-rule-arguments-read)> prem*;
      target' := <alltd(mark-rule-arguments-read)> target
    |}

  mark-rule-arguments-bind-source:
    Cast(var@Var(v), l@ListSort(_)) -> Cast(ArgBind(0), l)
    where
      rules(RenameArgument: VarRef(v) -> ArgRead(0){var})

  mark-rule-arguments-bind-source:
    As(var@Var(v), source) -> source
    where
      rules(RenameArgument: VarRef(v) -> ArgRead(0){var})

  mark-rule-arguments-read = RenameArgument

  type-of:
    ArgRead(i){v} -> <type-of> v

rules /* native operators */

  desugar-natives = innermost(desugar-native-ops + desugar-native-types)

  desugar-native-ops:
    Con(c, k*) -> NativeOp(c, k*)
    where
      <lookup-def(|Constructors()); lookup-prop(|ConsKind())> (c, <length> k*) => NativeOpCons()

  desugar-native-types:
    SimpleSort(s) -> NativeType(s, ty-str)
    where
      ty-def := <lookup-def(|Types())> s;
      NativeSort() := <lookup-prop(|SortKind())> ty-def;
      ty-str := <lookup-prop(|NativeTypeJString()); unquote(?'"')> ty-def

  desugar-native-types:
    SortFunCall(name, recv, arg*) -> NativeFunCall(<type-of> recv, name, recv, arg*)
  
  type-of:
    NativeOp(c, k*) -> ty
    where
      ConstructorType(_, ty) := <lookup-def(|Constructors()); lookup-prop(|Type())> (c, <length> k*)
  
  type-of:
    NativeFunCall(srecv, name, _, _) -> ty
    where
      s-def := <lookup-def(|Types())> srecv;
      f-def := <lookup-native-fun-def(|name)> s-def;
      FunctionType(_, ty) := <lookup-prop(|Type())> f-def

rules /* desugar rule conclusion/premise source */

  desugar-semantic-components = alltd(desugar-rule-conclusion-source); alltd(desugar-relation-premise-source)

  desugar-rule-conclusion-source:
    Rule(p*, infer, Relation(Reads(ro*), Source(t, rw*), rel, tgt)) -> Rule(p*, infer, Relation(Source(t, [ro*, rw*]), rel, tgt)) 

  desugar-relation-premise-source:
    Formula(Relation(Reads(ro*), Source(t, rw*), rel, tgt)) -> Formula(Relation(Source(t, [ro*, rw*]), rel, tgt))

rules /* list reductions */

  desugar-list-reductions = alltd(desugar-list-reduction)
  
  desugar-list-reduction:
    Formula(Relation(Source(t, rws), rel, target)) -> Formula(Relation(Source(ListSource(t, ty), rws), rel, target))
    where
      <type-of> t => ty@ListType(_)

rules /* TCO */

  desugar-rec-tco = alltd(desugar-rule-rec-tco)

  desugar-rule-rec-tco:
    Rule(p*, infer, rel@Relation(Source(Con(c, k*), _), NamedDynamicEmitted(_, arrow-name), _)) -> RecRule(p'*, infer, rel)
    where {| CurrentRelation:
      rules(CurrentRelation: _ -> (c, <length> k*, arrow-name));
      p'* := <desugar-prems-rec-tco(try(?succ))> p*;
      !succ // ensure we have rewritten at least one call
    |}
  
  desugar-rule-rec-tco:
    Rule(p*, infer, rel@Relation(Source(Cast(_, ty@ListSort(_)), _), NamedDynamicEmitted(_, arrow-name), _)) -> RecRule(p'*, infer, rel)
    where {| CurrentRelation:
      rules(CurrentRelation: _ -> (<rw-type> ty, -1, arrow-name));
      p'* := <desugar-prems-rec-tco(try(?succ))> p*;
      !succ // ensure we have rewritten at least one call
    |}
  
  desugar-prems-rec-tco(s) = is-list; try(at-last(desugar-prem-rec-tco(s)))
  
  desugar-prem-rec-tco(s) =
    MergePoint(id, PremiseBlock(desugar-prems-rec-tco(s)), PremiseBlock(desugar-prems-rec-tco(s)))

  desugar-prem-rec-tco(s):
    [Formula(Relation(src@Source(Con(c, k*), _), rel@NamedDynamicEmitted(_, arrow-name), tgt))] ->
      [Formula(RecRelation(src, rel, tgt))]
    where
      (c, <length> k*, arrow-name) := <CurrentRelation>;
      s

  desugar-prem-rec-tco(s):
    [Formula(Relation(src@Source(ArgRead(0), _), rel@NamedDynamicEmitted(_, arrow-name), tgt))] ->
      [Formula(RecRelation(src, rel, tgt))]
    where
      (_, _, arrow-name) := <CurrentRelation>;
      s
  
  desugar-prem-rec-tco(s):
    [Formula(Relation(src@Source(ListSource(_, lty), _), rel@NamedDynamicEmitted(_, arrow-name), tgt))] ->
      [Formula(RecRelation(src, rel, tgt))]
    where
      (lty, -1, arrow-name) := <CurrentRelation>; 
      s
  
