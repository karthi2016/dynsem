module ds2ds/desugar-varschemes

imports
  signatures/-
  lib-ds
  analysis/-
  ds

rules
  
  desugar-varschemes-module =
    ?Module(_, _);
    m-in-analysis(desugar-varschemes; unrename-all);
    unmark-vars

  desugar-varschemes = alltd(desugar-varscheme)
  
  desugar-varscheme:
    VarLabelComp(v) -> LabelComp(Label(<def-get-name> scheme-def), v)
    where
    	scheme-def := <get-unique-matching-varscheme(get-all-component-defs)> v
  
  // do not expand the variable schemes in as-bound variables
  // expanding to a cast will lead to a malformed AST
  desugar-varscheme = As(id, desugar-varschemes) 
  
  desugar-varscheme:
    var@Var(v) -> Cast(var, <derw-type> scheme-ty)
    where
      scheme-def := <get-unique-matching-varscheme(get-all-varschemes)> v;
      scheme-ty := <lookup-prop(|Type())> scheme-def
rules
  
  resugar-varschemes-module =
    ?Module(_, _);
    m-in-analysis(resugar-varschemes; unrename-all);
    unmark-vars
  
  resugar-varschemes = alltd(resugar-varscheme)
  
  resugar-varscheme:
    LabelComp(Label(lname), v) -> VarLabelComp(v)
    where
      scheme-def := <get-unique-matching-varscheme(get-all-component-defs)> v;
      <def-get-name> scheme-def => lname

      