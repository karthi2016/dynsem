module ds2ds/desugar-varschemes

imports
  signatures/-
  lib-ds
  analysis/-
  ds

rules
  
  desugar-varschemes-module :
    mod@Module(_, _) -> Module($[[name]_varschemes], section'*)
    where
      <m-in-analysis(desugar-varschemes; unrename-all); unmark-vars> mod => Module(name, section'*)

  desugar-varschemes = alltd(desugar-varscheme)
  
  desugar-varscheme:
    VarLabelComp(v) -> LabelComp(<derw-type> scheme-ty, v)
    where
    	scheme-def := <get-unique-matching-varscheme> v;
    	scheme-ty := <lookup-prop(|Type())> scheme-def
  
  desugar-varscheme:
    var@Var(v) -> Cast(var, <derw-type> scheme-ty)
    where
      scheme-def := <get-unique-matching-varscheme> v;
      scheme-ty := <lookup-prop(|Type())> scheme-def
    	


