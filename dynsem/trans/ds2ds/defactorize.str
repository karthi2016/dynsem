module ds2ds/defactorize

imports
  signatures/-
  lib-ds
  analysis/mark-references

strategies

  defactorize-module =
    Module(!$[[<id>]_unfactorized],
      alltd(defactorize-rule)
    )

rules
  
  defactorize-rule =
    ?Rule(_, _, _);
    {| ReadCount:
      mark-var-defs-in-rule;
      repeat(
        count-varreads-rule;
        defactorize-rule-w-progressed
      )
    |}
  
  defactorize-rule-w-progressed =
    insert-wildcards-rule(try(?progress));
    hoist-patternmatches-rule(try(?progress));
    lower-termbuilds-rule(try(?progress));
    where(!progress)
  
  count-varreads-rule:
    r@Rule(_, _, _) -> r
    where
      <collect-all(?Var(_)); nub; map(init-count-varread)> r;
      <alltd(count-varread)> r
  
  init-count-varread = ?Var(v); rules(ReadCount: v -> 0)
  
  count-varread = 
    ?VarRef(v);
    reads := <ReadCount; inc> v;
    rules(ReadCount: v -> reads)

  insert-wildcards-rule(record-progress) = alltd(insert-wildcards(record-progress))
  
  insert-wildcards(record-progress):
    Var(v) -> Wld()
    where
      <ReadCount> v => 0;
      record-progress
  
  hoist-patternmatches-rule(record-progress):
    Rule(p*, infer, rel) -> r'
    where {| PropagatePatternMatch:
      p'* := <eliminate-patternmatch-premises> p*;
      r' := <alltd(replace-singletonwrite(record-progress))> Rule(p'*, infer, rel)
    |}

  eliminate-patternmatch-premises =
    filter(
      MergePoint(id, PremiseBlock(eliminate-patternmatch-premises), PremiseBlock(eliminate-patternmatch-premises))
      <+ not(eliminate-patternmatch-premise))

  eliminate-patternmatch-premise =
    ?Formula(Match(VarRef(v), rhs));
    <ReadCount> v => 1;
    rules(PropagatePatternMatch: Var(v) -> rhs)

  replace-singletonwrite(record-progress):
    var@Var(v) -> t
    where
      <repeat1(PropagatePatternMatch)> var => t;
      record-progress

  lower-termbuilds-rule(record-progress):
    Rule(p*, infer, rel) -> r' 
    where {| PropagateTermBuild:
      p'* := <filter(not(eliminate-termbuild-premise))> p*;
      r' := <alltd(replace-singletonread(record-progress))> Rule(p'*, infer, rel)
    |}
  
  eliminate-termbuild-premise =
    ?Formula(Match(lhs, Var(v)));
    <ReadCount> v => 1;
    rules(PropagateTermBuild: v -> lhs)
  
  replace-singletonread(record-progress):
    VarRef(v) -> t
    where
      <PropagateTermBuild> v => t;
      record-progress
      

