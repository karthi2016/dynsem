module ds2ds/copy-propagation

imports
  signatures/-

rules
  
  copy-propagation-module = Module(!$[[<id>]_copyprop], alltd(copy-propagation-rule))

  copy-propagation-rule:
    Rule(prem*, infer, Relation(reads, source, arrow, Target(rhs, tc*))) ->
      Rule(prem'*, infer, Relation(reads, source, arrow, Target(rhs', tc'*)))
    where {| PropagateCopy:
      prem'* := <filter(copy-propagation)> prem*;
      rhs' := <copy-propagation-prop> rhs;
      tc'* := <copy-propagation-prop> tc*
    |}
  
  copy-propagation-premises = is-list;  {| PropagateCopy: filter(copy-propagation) |}
  
  copy-propagation =
    not(copy-propagation-prop; copy-propagation-rec);
    copy-propagation-prop
  
  copy-propagation-prop = alltd(VarRef(PropagateCopy))
  
  copy-propagation-rec:
    f@Formula(Match(VarRef(var1), VarRef(var2))) -> f
    where
      rules(PropagateCopy:+ var2 -> var1)


