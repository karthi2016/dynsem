module ds2ds/sugar

imports
	ds
	ds-main
	
signature
  constructors
    MapExtend : Map * Map -> Map
    MapEntry : Map -> Bind
	
rules // desugaring  
	
	desugar-functions :
		Signatures(sigs1) -> 
		Signatures(
			[ Sorts(ss)
			, ArrowDeclarations(as) 
			| sigs2])
		where <collect(extract-arrow); unzip> sigs1 => (as@[_|_], ss);
		      <alltd(desugar-fundecl)> sigs1 => sigs2
		      
  desugar-fundecl :
  	FunDecl(x, ts, t) -> ConsDecl(x, ts, t_source, NoAnnos())
  	where 
  		t_name := $[[x]__Arrow];
  	  t_source := SimpleSort(t_name)

  extract-arrow :
  	FunDecl(x, ts, t) -> (DefaultArrowDecl(t_source, t), SortDecl(t_name))
  	where 
  		t_name := $[[x]__Arrow];
  	  t_source := SimpleSort(t_name)
  	  	
  desugar-top = 
  	alltd(desugar-functions);
  	innermost(ds-desugar); 
  	alltd(rules-concat)
  
  ds-desugar :
    NativeNoArgFunctionDecl(name, type) -> NativeFunctionDecl(name, [], type)
  
  ds-desugar :
    NullaryConsDecl(c, s, annos) -> ConsDecl(c, [], s, annos)
  
  ds-desugar :
    NoAnnos() -> Annos([])
  
  ds-desugar :
    NullaryNativeConsDecl(c, s) -> NativeConsDecl(c, [], s)

  ds-desugar :
    NullaryNativeOpDecl(c, s) -> NativeOpDecl(c, [], s)
  
  ds-desugar :
    RuleW(form, prem*) -> Rule(prem*, "---", form)
  
  ds-desugar :
    DefaultArrowDecl(l-ty, r-ty) -> ArrowDecl(l-ty, "default", r-ty)
  
  ds-desugar :
    Dynamic() -> NamedDynamic("default")
  
  ds-desugar :
    DynamicEmitted(e) -> NamedDynamicEmitted(e, "default")
  
  ds-desugar :
    NamedDynamic(n) -> NamedDynamicEmitted([], n)
  
  ds-desugar :
    List(x*) -> ListTail(x*, List([]))
    where <not(?[])> x*

  ds-desugar :
    ListTail([x | xs*], xxs) -> ListTail([x], ListTail(xs*, xxs))
    where
      <not(?[])> xs*

  ds-desugar :
    NoReads() -> Reads([])

  ds-desugar :
    Source(lhs) -> Source(lhs, [])

  ds-desugar :
    Target(rhs) -> Target(rhs, [])

  ds-desugar :
    Axiom(form) -> Rule([], "---------", form)
    
  ds-desugar :
    IsValue(term) -> TermEq(Con("IsValue",[term]),Con("True",[]))

  ds-desugar :
    Map([b | b*@[_|_]]) -> MapExtend(Map([b]), Map(b*))
    
  ds-desugar :
    Map([MapEntry(x)]) -> x

  ds-desugar :
    Strict(con,params1,params2) -> list
     where Cons(_,Cons(_,_)) := params1;
           list := <map( \ param -> Strict(con,[param],params2) \)> params1

  ds-desugar :
    Seqstrict(con,params1,params2) -> list
     where Cons(_,Cons(_,_)) := params1;
           list := <map( \ param -> Seqstrict(con,[param],params2) \)> params1
     // this converts a Rule into a list of Rules
     // so they must later be concatenated, post-desugaring:
  
  rules-concat :
  	 Rules(list) -> Rules(list1)
  	 where list1 := <map(?Cons(_,_) <+ ( \ n -> Cons(n,Nil()) \)); concat> list

rules // sugaring
  
  sugar-all = innermost(ds-sugar)
  
  ds-sugar :
    NativeFunctionDecl(name, [], type) -> NativeNoArgFunctionDecl(name, type)
  
  ds-sugar :
    ConsDecl(c, [], s, annos) -> NullaryConsDecl(c, s, annos)
  
  ds-sugar :
    Annos([]) -> NoAnnos()
  
  ds-sugar :
    NativeConsDecl(c, [], s) -> NullaryNativeConsDecl(c, s)

  ds-sugar :
    NativeOpDecl(c, [], s) -> NullaryNativeOpDecl(c, s)
  
  ds-sugar :
  	Rule([],infer,conc) -> Axiom(conc)

  ds-sugar :
  	Reads([]) -> NoReads()

  ds-sugar :
  	NamedDynamicEmitted([], name) -> NamedDynamic(name)

  ds-sugar :
  	Target(term,[]) -> Target(term)

  ds-sugar :
  	Source(term,[]) -> Source(term)

  ds-sugar :
    TermEq(Con("IsValue",[term]),Con("True",[])) -> IsValue(term)
    
  ds-sugar :
    Rule(prem*, _, form) -> RuleW(form, prem*)

  ds-sugar :
  	MatchedVar(v) -> Var(v)
  
