module ds

imports Common Lexical Module Signatures
 
context-free start-symbols Module

templates // rules
     
  ModuleSection.Rules = [
    rules 
    
      [Rule*; separator="\n\n"]
  ] 
  
  Rule.Strict = [strict [ID] [INT+; separator=","]/[INT]]
  Rule.Seqstrict = [seqstrict [ID] [INT+; separator=","]/[INT]]
    
  Rule.Axiom = [[Formula]]
      
  Rule.Rule = [
    [Premise+; separator=",\n"]
    [INFER]
    [Formula]
  ]

  Rule.RuleW = [
    [Formula]
    where
      [Premise+; separator=",\n"]
  ]
     
  Premise.Formula = [[Formula]]
  Premise.AllFail = <all-fail>

  Premise.MergePoint = 
    [
      [Premise]
      <
      {
        [Premise+; separator=",\n"]
      }
      +
      {
        [Premise+; separator=",\n"]
      }
    ]

  ID = <rules> {reject}
  ID = <fresh> {reject}
  
templates // formulas

  Formula.Match    = [[Term] => [Term]]
  Formula.NMatch   = [[Term] =!=> [Term]] 
  Formula.TermEq   = [[Term] == [Term]]  
  Formula.TermNeq  = [[Term] != [Term]]
  
  Formula.Relation = [[Reads][Source] [Rel] [Target]]
  Formula.IsValue  = [value([Term])]
  
  Reads.NoReads = []
  Reads.Reads   = [[LabelComp+; separator=", "] |- ]
  
  Source.Source = [[Term]]
  Source.Source = [([Term], [LabelComp+; separator=", "])]

  Target.Target = [[Term]]
  Target.Target = [([Term], [LabelComp+; separator=", "])]
  
  Rel.NamedDynamic = [- [ID] ->]
  Rel.Dynamic = [-->]
  
  Rel.NamedDynamicEmitted = [- [LabelComp+; separator=", "] -[ID]->]
  Rel.DynamicEmitted = [- [LabelComp+; separator=", "] ->]
  
  LabelComp.LabelComp = [[Type] [Term]]
  
templates // terms

  // Term.MatchVar = [[ID]] {avoid}
  // Term.MatchVar = [[LID]] {avoid}

  Term.VarRef   = [[ID]] {avoid}
  Term.VarRef   = [[LID]] {avoid}

  Term.Var      = [[ID]]
  Term.Var      = [[LID]]
  
  Term.String   = [[STRING]]
  Term.Int      = [[INT]]
  Term.Con      = [[ID]([Term*; separator=", "])]
  Term.List     = <[ <Term*; separator=", "> ]>
  Term.ListTail = <[ <Term*; separator=", "> | <Term>]>
  
  Term.Fresh  = [fresh]
  Term.Map    = [{[Entry*;separator=", "]}]
  Entry.Bind  = [[Term] |--> [Term]] 
  Entry.Binds = [[Term] |*-> [Term]]
  Entry.MapEntry = [[Term]]
  
  Term.MapSelect = <<Term>[<Term>]>
  Term.MapExtend = [[Term] + [Term]] {assoc}
  //Term.MapExtend = [[Term] + [Term]]

  Term.SortFunCall = <<ID>[<Term>](<Term*; separator=", ">)>

context-free priorities

  Term.MapSelect > Term.MapExtend
  
  
  
	