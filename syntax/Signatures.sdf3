module Signatures

imports Common

templates

  ModuleSection.Signatures = <
    signature
        <SignatureSection*; separator="\n">>
  
  SignatureSection.Sorts = <
    sorts
      <SortDecl*; separator="\n">>
  
  SortDecl.SortDecl = <<ID> <SignatureAnnos?>>
  SortDecl.InjDecl = [[ID] -> [ID] [SignatureAnnos?]]
  
  SignatureSection.Constructors = <
    constructors
      <ConsDecl*; separator="\n">>

  ConsDecl.ConsDecl = [[ID] : [Type*; separator=" * "] -> [Type] [SignatureAnnos?]]

  Type.SimpleSort = <<ID>>
  Type.ListSort = <List(<ID>)> 
  
  SignatureAnnos.SignatureAnnos = <{ <SignatureAnno*; separator=", "> }>

  SignatureAnno.NativeTermAnno = <native-term>
  SignatureAnno.NativeFunAnno  = <native-operator>
  SignatureAnno.InternalAnno   = <semantic-operator>
  

