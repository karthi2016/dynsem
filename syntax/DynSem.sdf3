module DynSem

imports Common Lexical

context-free start-symbols Start

templates // rules
  
  Start.File = [[Item*; separator="\n\n"]]
  
  Item.Rules = [
    rules 
    
      [Rule*; separator="\n\n"]
  ]
  
  Rule.Strict = [strict [ID] [INT+; separator=","]/[INT]]
  Rule.Seqstrict = [seqstrict [ID] [INT+; separator=","]/[INT]]
    
  Rule.Axiom = [[Formula]]
      
  Rule.Rule = [
    [Premise+; separator=",\n"]
    [INFER]
    [Formula]
  ]
  
  Premise.Formula = [[Formula]]

  ID = <rules> {reject}
  
templates // formulas

  Formula.TermEq = [[Term] = [Term]]
  Formula.Relation = [[Reads][Source] [Rel] [Target]]
  Formula.IsValue = [value([Term])]
  
  Reads.NoReads = []
  Reads.Reads = [[LabelComp+; separator=", "] |- ]
  
  Source.Source = [[Term]]
  Source.Source = [([Term], [LabelComp+; separator=", "])]

  Target.Target = [[Term]]
  Target.Target = [([Term], [LabelComp+; separator=", "])]
  
  Rel.Dynamic = [-->]
  Rel.DynamicEmitted = [-- [LabelComp+; separator=", "] -->]
  
  LabelComp.LabelComp = [[ID] [Term]]

templates // terms

  Term.Con    = [[ID]([Term*; separator=", "])]
  Term.Var    = [[ID]]
  Term.List   = <[ <Term*; separator=", "> ]>
  Term.Map    = [{[Entry*;separator=", "]}]
  Term.String = [[STRING]]
  
  Entry.Bind = [[Term] |--> [Term]]
	